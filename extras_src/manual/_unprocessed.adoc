

== Have the game banner show at the start

To show the game banner at the start, after an optional intro text, you must add the text "

after the START AT clause, for example:

[source,alan]
--------------------------------------------------------------------------------
START AT room1.

DESCRIBE banner.
--------------------------------------------------------------------------------

or:

[source,alan]
--------------------------------------------------------------------------------
START AT room1.
--------------------------------------------------------------------------------

"This is the (optional) intro text at the start of the game, before the first

location description."

[source,alan]
--------------------------------------------------------------------------------
DESCRIBE banner.
--------------------------------------------------------------------------------

The following attributes should be added to the

[source,alan]
--------------------------------------------------------------------------------
HAS title "The Baffling Case Of Mrs Wells".

HAS subtitle "An interactive mystery".

HAS author "Sam".

HAS year 2017.

HAS version "1".
--------------------------------------------------------------------------------

Leaving the subtitle line out and setting the

now, these attributes would produce the following kind of banner text:

The Baffling Case Of Mrs Wells

An interactive mystery

Â© 2017 by Sam

Programmed with the ALAN Interactive Fiction Language v3.0

Version 1

All rights reserved

// PAGE 100 //



== Runtime messages

Many of the runtime messages built into ALAN have been altered in the library from their default wording as stated

in the ALAN manual. This is to ensure that plural is handled correctly and that there are no clashes between first and

second person. The first person of some default wordings (for example "I don't know the word "\$1") is changed to a

more passive or impersonal formulation. To edit these for your game, open 'lib_messages.i' and edit the wanted

message(s) there.

[source,alan]
--------------------------------------------------------------------------------
MESSAGE

AFTER_BUT: "You must give at least one object after '\$1'."

AGAIN: ""

BUT_ALL: "You can only use '\$1' AFTER '\$2'."

CAN_NOT_CONTAIN: "\$+1 can not contain \$+2."

CANT0: "You can't do that."

-- note that the fifth token in CANT0 is a zero, not an 'o'.

CARRIES:

IF parameter1 = hero

THEN "You are carrying"

ELSE

IF parameter1 IS NOT plural

THEN "\$+1 carries"

ELSE "\$+1 carry"

END IF.

END IF.

CONTAINMENT_LOOP:

"Putting \$+1 in"

IF parameter1 IS NOT plural

THEN "itself"

ELSE "themselves"

END IF.

"is impossible."

CONTAINMENT_LOOP2:

"Putting \$+1 in \$+2 is impossible since \$+2 already"

IF parameter2 IS NOT plural

THEN "is"

ELSE "are"

END IF.

"inside \$+1."

'CONTAINS':

IF parameter1 IS NOT plural

THEN "\$+1 contains"

ELSE "\$+1 contain"

END IF.

CONTAINS_COMMA: "\$01,"

CONTAINS_AND: "\$01 and"

CONTAINS_END: "\$01."

// PAGE 101 //



EMPTY_HANDED:

IF parameter1 = hero

THEN "You are empty-handed."

ELSE

IF parameter1 IS NOT plural

THEN "\$+1 is empty-handed."

ELSE "\$+1 are empty-handed."

END IF.

END IF.

HAVE_SCORED: "You have scored \$1 points out of \$2."

IMPOSSIBLE_WITH: "That's impossible with \$+1."

IS_EMPTY:

IF parameter1 IS NOT plural

THEN "\$+1 is empty."

ELSE "\$+1 are empty."

END IF.

MORE: "<More>"

MULTIPLE: "You can't refer to multiple objects with '\$v'."

NO_SUCH: "You can't see any \$1 here."

NO_WAY: "You can't go that way."

NOT_MUCH: "That doesn't leave much to \$v!"

NOUN: "You must supply a noun."

NOT_A_SAVEFILE: "That file does not seem to be an Alan game save

file."

QUIT_ACTION: "Do you want to RESTART, RESTORE, QUIT or UNDO? "

-- these four alternatives are hardwired to the interpreter and cannot be changed.

REALLY: "Are you sure (press ENTER to confirm)?"

RESTORE_FROM: "Enter file name to restore from"

SAVE_FAILED: "Sorry, save failed."

SAVE_MISSING: "Sorry, could not open the save file."

SAVE_NAME: "Sorry, the save file did not contain a save for this

adventure."

SAVE_OVERWRITE: "That file already exists, overwrite (y)?"

SAVE_VERSION: "Sorry, the save file was created by a different

version."

SAVE_WHERE: "Enter file name to save in"

SEE_START:

IF parameter1 IS NOT plural

THEN "There is \$01"

ELSE "There are \$01"

END IF.

SEE_COMMA: ", \$01"

SEE_AND: "and \$01"

SEE_END: "here."

NO_UNDO: "No further undo available."

UNDONE: "'\$1' undone."

UNKNOWN_WORD: "The word '\$1' is not understood."

WHAT: "That was not understood."

WHAT_WORD: "It is not clear what you mean by '\$1'."

WHICH_PRONOUN_START: "It is not clear if you by '\$1'"

// PAGE 102 //



WHICH_PRONOUN_FIRST: "mean \$+1"

WHICH_START: "It is not clear if you mean \$+1"

WHICH_COMMA: ", \$+1"

WHICH_OR: "or \$+1."
--------------------------------------------------------------------------------

== Default attributes used in the standard library

The attributes in the following list are pre-defined in the library. When you coin your own attributes for your game,

please be aware that these attributes already exist. Using any of the attributes listed below for your own purposes

doesn't necessarily cause any problems, but if problems arise, it's likely because of their being used in the library.

This attribute is added to every ENTITY:

[source,alan]
--------------------------------------------------------------------------------
NOT plural.
--------------------------------------------------------------------------------

These attributes are added to every THING:

[source,alan]
--------------------------------------------------------------------------------
IS examinable.

inanimate.

movable.

open.

reachable.

-- See also 'distant' below

takeable.

HAS allowed {null_object}.

-- You can only put an object in a container if the object

-- is in the 'allowed' set of the container.

HAS ex "".

-- an alternative to using "VERB examine DOES..."

HAS matching_key null_key.

-- All lockable objects need a matching key to lock/unlock them.

-- "null_key" is a default dummy that can be ignored.

HAS text "".

HAS weight 0.

-- Actors and objects will have different weight values, see below

NOT broken.

NOT distant.

-- Usage: you can for example talk to a "not reachable" actor but

-- not to a "distant" one.

-- You can also throw things in, to or at a not reachable target

-- but not to a distant one.

-- The other verbs where the action succeeds if the object is

-- not reachable are: dive_in, fire_at, kill_with, read, and

// PAGE 103 //



-- shoot

-- Default response for not reachable things: "The \[thing\] is out

-- of your reach."

-- Default response for distant things: "The \[thing\] is too far

-- away."

NOT drinkable.

NOT edible.

NOT fireable.

-- can (not) be used as a firearm

NOT lockable.

NOT locked.

NOT 'on'.

NOT openable.

NOT readable.

NOT scenery.

-- has special responses for 'ask_for', 'examine', 'take' and

-- 'take_from', behaves like a normal object otherwise.

NOT wearable.

NOT writeable.

CAN NOT talk.

These attributes are added to every ACTOR:

IS wearing {null_clothing}.

-- By default, actors are not described as wearing any specific

-- clothing. null_clothing is a default dummy value that can be

-- ignored.

HAS weight 50.

-- If something has the weight value of 50 or more, it cannot

-- be lifted or taken.

NOT following.

-- not following the hero character by default

NOT inanimate.

NOT named.

NOT compliant.

NOT sitting.

NOT lying_down.

The code for CLOTHING objects adds these attributes, used only internally in the library, to every actor:

IS tempcovered 0.

IS wear_flag 0.

IS sex 0.
--------------------------------------------------------------------------------

// PAGE 104 //



These attributes are added to every OBJECT:

[source,alan]
--------------------------------------------------------------------------------
HAS weight 5.

-- This is the default weight of every object, whether takeable

-- or NOT takeable. However, the library by itself

-- doesn't define any limit for containers. If the game author

-- wants to have a limit to how many objects a container can hold,

-- the author must set this limit by themselves.

Attributes added to specific classes of objects:

These attributes are added to every CLOTHING object:

IS wearable.

IS NOT donned.

-- = not worn by an NPC

IS sex 0.

IS headcover 0.

IS handscover 0.

IS feetcover 0.

IS topcover 0.

IS botcover 0.

The following attribute is defined for every DOOR object:

HAS otherside door.

The following attributes are added to every LIGHTSOURCE object:

IS natural.

IS NOT lit.

The following attribute is added to every WEAPON:

IS NOT fireable.

The following attributes are added to every LOCATION:

IS lit.

HAS visited 0.

HAS described 0.

HAS nested {nowhere}.
--------------------------------------------------------------------------------

// PAGE 105 //



The score notification coding uses the following attributes:

[source,alan]
--------------------------------------------------------------------------------
HAS oldscore 0.

IS notify_on.

IS NOT seen_notify.
--------------------------------------------------------------------------------

Finally, for restricted actions, there is an attribute defined to correspond to every library verb. (See the list on p. 61-.)

== Translating to other languages

To translate the ALAN system and library to other languages, you should

1) translate all the messages in the file 'lib_definitions.i':

* the two messages for the hero

* the two messages for dark locations

* all illegal parameter messages

* all verb check messages

* the message for implicit taking

* the message lines for the banner instance where applicable

2) translate all the "CAN \[verb\]" attributes in the file 'lib_definitions.i'.

3) translate the verb syntaxes in 'lib_verbs.i' (not parameters and the ELSE parts).

For example for the verb

[source,alan]
--------------------------------------------------------------------------------
SYNTAX attaquer = attaquer (target)

WHERE target ISA THING

ELSE

IF target IS NOT plural

THEN SAY illegal_parameter_sg OF my_game.

ELSE SAY illegal_parameter_pl OF my_game.

END IF.
--------------------------------------------------------------------------------

Also, translate the verb names, for example

[source,alan]
--------------------------------------------------------------------------------
...

VERB attaquer DOES ...
--------------------------------------------------------------------------------

4) translate the verb outcomes for class objects (what happens after DOES or DOES ONLY) in 'lib_classes.i'.

5) translate the direction names, their synonyms and the few marginal verb outcomes for indoor and outdoor objects

in 'lib_locations.i'

// PAGE 106 //



6) translate the runtime messages in 'lib_messages.i'.

Now, every possible response and message in the game is shown in the target language, and it is possible for the

player to issue commands in the target language.

It's up to the translator to decide whether to translate any of the library-defined default attributes.

== Short examples

1) A very short complete game using minimal obligatory imports and coding. Here, the hero must go from room1 north

to room2 and eat an apple to win the game.

[source,alan]
--------------------------------------------------------------------------------
IMPORT 'library.i'.

THE my_game ISA DEFINITION_BLOCK

END THE.

THE room1 ISA LOCATION

DESCRIPTION "North to room2."

EXIT north TO room2.

END THE.

THE room2 ISA LOCATION

DESCRIPTION "South to room1."

EXIT south TO room1.

END THE.

THE apple ISA OBJECT AT room2

IS edible.

VERB eat

DOES "Congratulations!" QUIT.

END VERB.

END THE.

START AT room1.

DESCRIBE banner.
--------------------------------------------------------------------------------

(This game wouldn't actually need the library at all; it would be even shorter to code:)

// PAGE 107 //



[source,alan]
--------------------------------------------------------------------------------
THE room1 ISA LOCATION

DESCRIPTION "North to room2."

EXIT north TO room2.

END THE.

THE room2 ISA LOCATION

DESCRIPTION "South to room1."

EXIT south TO room1.

END THE.

THE apple ISA OBJECT AT room2

VERB eat

DOES "Congratulations!" QUIT.

END VERB.

END THE.

START AT room1.
--------------------------------------------------------------------------------

In this latter case, though, the player wouldn't for example be able to examine him-/herself, trying to go any other

direction, take inventory, try various things with the apple, quit properly, etc.

2) Here, the player must get a candy from the kitchen and give it to a crying child in the nursery to win the game.

[source,alan]
--------------------------------------------------------------------------------
IMPORT 'library.i'.

THE my_game ISA DEFINITION_BLOCK

END THE.

THE nursery ISA ROOM

DESCRIPTION "The kitchen is to the east."

EXIT east to kitchen.

END THE.

THE child ISA PERSON AT nursery

DESCRIPTION "There is a crying child here."

VERB give

WHEN recipient

DOES ONLY

IF obj = candy

THEN "You give the candy to the child who stops

crying and starts licking it happily."

QUIT.

END IF.

END VERB.

END THE.

// PAGE 108 //



THE kitchen ISA ROOM

DESCRIPTION "You can go west, back to the nursery."

EXIT west TO nursery.

END THE.

THE table ISA SUPPORTER AT kitchen

IS NOT takeable.

END THE.

THE candy ISA OBJECT IN table

IS edible.

END THE.

START AT nursery.
--------------------------------------------------------------------------------

Examples 3-4 below show mainly different variations of the

3) In this example of defining the

'climb' and 'take_from'. In addition, the author has added a check and a response of his/her own to 'take_from':

[source,alan]
--------------------------------------------------------------------------------
THE my_game ISA DEFINITION_BLOCK

VERB eat

DOES ONLY "You don't feel like eating anything in this game."

END VERB.

VERB climb

DOES ONLY "Let's just stay on the ground, shall we?"

END VERB.

VERB take_from

WHEN obj

CHECK COUNT ISA ACTOR, AT hero = 1

-- ( = the hero himself)

ELSE "You don't want to take anything while somebody

might be looking."

DOES "Triumphantly, you fish" SAY THE obj. "out of"

SAY THE holder. "."

END VERB.

END THE.
--------------------------------------------------------------------------------

// PAGE 109 //



4) Here, the author uses the automatic formulation for the game title, author, and other information:

[source,alan]
--------------------------------------------------------------------------------
THE my_game ISA DEFINITION_BLOCK

HAS title "The Lost Treasure".

HAS subtitle "An interactive treasure hunt".

HAS author "Sam".

HAS year 2019.

HAS version "1".

END THE.

THE garden ISA LOCATION

DESCRIPTION "..."

END THE.

START AT garden.

DESCRIBE banner.
--------------------------------------------------------------------------------

5) Here, the game author has added a check of his own to the library-defined

parameter message for the verbs

[source,alan]
--------------------------------------------------------------------------------
THE my_game ISA DEFINITION_BLOCK

VERB drink

CHECK hero IS thirsty

ELSE "You don't feel like drinking anything right now."

END VERB.

HAS illegal_parameter_there "You can't \$v there.".

END THE.
--------------------------------------------------------------------------------

6) A complete example game with locked doors and keys. This code reintroduces the situation used in example 1,

with a locked door and two keys added.

[source,alan]
--------------------------------------------------------------------------------
IMPORT 'lib_classes.i'.

IMPORT 'lib_definitions.i'.

IMPORT 'lib_locations.i'.

IMPORT 'lib_messages.i'.

IMPORT 'lib_verbs.i'.

THE my_game ISA DEFINITION_BLOCK

END THE.

// PAGE 110 //



THE room1 ISA LOCATION

DESCRIPTION "North to room2."

EXIT north TO room2

CHECK locked_door_1 IS open

ELSE "The door to the north is on the way."

END EXIT.

END THE.

THE locked_door_1 ISA DOOR AT room1

DESCRIPTION ""

NAME door

HAS otherside locked_door_2.

IS lockable. IS locked.

HAS matching_key silver_key.

END THE.

THE silver_key ISA OBJECT AT room1

NAME silver key

END THE.

THE brass_key ISA OBJECT AT room1

NAME brass key

END THE.

THE room2 ISA LOCATION

DESCRIPTION "South to room1."

EXIT south TO room1

CHECK locked_door_2 IS open

ELSE "The door to the south is on the way."

END EXIT.

END THE.

THE locked_door_2 ISA DOOR AT room2

DESCRIPTION ""

NAME door

END THE.

THE apple ISA OBJECT AT room2

IS edible.

VERB eat

DOES "Congratulations!" QUIT.

END VERB.

END THE.

START AT room1.

DESCRIBE banner.
--------------------------------------------------------------------------------

// PAGE 111 //
