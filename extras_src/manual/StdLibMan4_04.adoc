////
********************************************************************************
*                                                                              *
*                     ALAN Standard Library User's Manual                      *
*                                                                              *
*                   PART Library Verbs Â» Working with Verbs                    *
*                                                                              *
********************************************************************************
////


[[ch.working-with-verbs]]
= Working with Verbs

The library defines numerous verbs that can be readily used in-game with any objects or actors.
For example, if you implement

[source,alan]
--------------------------------------------------------------------------------
THE ball ISA OBJECT AT garden
END THE ball.
--------------------------------------------------------------------------------

the library enables you to [.play]#&gt; _examine ball_#, [.play]#&gt; _take ball_#, [.play]#&gt; _throw ball_#, [.play]#&gt; _kick ball_#, etc.
(with more or less successful default outcomes, depending on the verb) without your having to code anything specific to make this possible.


[[sec.verbs-outcomes]]
== Verbs Outcomes

If you try the various library commands, inside an adventure game, you will notice that some commands will succeed to carry out the expected action(s), while others will report a failure message, without carrying out any action.
For example, the response to [.play]#&gt; _drop [object]_# will be [.play]#Dropped.#, with the carried object being removed from the hero's inventory and placed in the location; whereas the response to [.play]#&gt; _attack [thing]_# will be [.play]#Resorting to brute force is not the solution here.#.

Which actions are allowed to succeed, and which not, is based on what is the most reasonable and expected outcome for any given action -- i.e. the outcome that game authors unlikely need to edit, except under special circumstances.

// @TODO: Add XRef to "further on in this chapter", pointing to actual section
//        dealing with verb overrides on `my_game`.

You're strongly encouraged to experiment with the different verbs, to learn whether the default outcome of a particular action is suitable for your game -- and if it isn't, redefine the outcome of that verb on the `my_game` instance.
You'll learn the details on how to achieve this further on in this chapter.

// PAGE 65 //


[[sec.alternative-syntaxes]]
== Alternative Syntaxes

If you wish to add flexibility to your game by allowing alternative syntaxes for certain verbs, you can easily do it directly in your adventure source file, without having to edit the library sources.

// @REMOVED: The following sentence was temporarily commented out, until we have
//           a proper XRef to where all verbs syntaxes can be found.
//           (most likely, the Verbs Table, but it needs to be checked first)

// Elsewhere in this manual you'll find all verb syntaxes listed.

Suppose that you wished to improve the syntax of the `talk_to` verb.
First of all, you'll need find out what the default syntax of the `talk_to` verb looks like.
You can look the verb up in the <<table.verbs,Verbs Table>> found in this manual, which provides the base syntax of each verb in a dedicated column; or you can peek inside the library source file {lib_verbs}, where are all verbs and syntaxes are defined.

In this case, the default syntax of `talk_to` is:

[source,alan, role="lib"]
--------------------------------------------------------------------------------
SYNTAX talk_to = talk 'to' (act)
--------------------------------------------------------------------------------

enabling commands like:

[example,role="gametranscript"]
================================================================================
&gt; _talk to man_
&gt; _talk to the teacher_
&gt; _talk to Daniel_
================================================================================


Now let's imagine that you wanted to extend the syntax so that it would be possible for the player to type:

[example,role="gametranscript"]
================================================================================
&gt; _talk man_
================================================================================


or just:

[example,role="gametranscript"]
================================================================================
&gt; _t man_
================================================================================


In other words, being able to mention the character with whom to talk, directly after the verb, without requiring the '`to`' preposition.

The easiest way to accomplish this is by providing an additional syntax for `talk_to` in your game source file:

[source,alan]
--------------------------------------------------------------------------------
SYNTAX  talk_to = talk (act).
        talk_to = t (act).
--------------------------------------------------------------------------------

This syntax declaration must be written outside of the `my_game` instance, in the main context of your adventure source code.
This additional syntax declaration won't replace the original syntax for `talk_to`, defined in the library; it would still be possible for the player to type [.play]#&gt; _talk to man_#, as well as using your newly defined alternative syntaxes.


== Replacing Syntaxes

Carrying on with the hypothetical `talk_to` example from the previous section, suppose you wished to replace the original syntax altogether, only allowing player to use your alternative syntax.
In order to achieve this, you'll need to carry out the following steps.

In your source adventure, suppress the action associated with the original syntax defined by the library:

[[warn.my_game-verbs]]
[WARNING]
.Re-Defining Verbs on `my_game`???
================================================================================
We need to check if verbs defined on `my_game` really work as described here.

* Do they?
* _Why should_ they?

The `my_game` is just an instance of `DEFINITION_BLOCK`, which is a `location` class, which the Hero _never enters into_; so, why should these verbs come into effect during play if they're never in scope?

Maybe these texts are referencing older version of the library, which no longer apply to the its later versions.
We need to test if this actually works, and if it does we need to explain why, otherwise we'll need to amend or purge similar examples and instructions from this Manual.

(See link:https://github.com/AnssiR66/AlanStdLib/issues/115[Issue #115] for more info.)
================================================================================

[source,alan]
--------------------------------------------------------------------------------
THE my_game ISA DEFINITION_BLOCK
  VERB talk_to
    DOES ONLY "To talk to someone, type ""talk [person]""
               or just ""t [person]""."
  END VERB.
END THE my_game.
--------------------------------------------------------------------------------

// PAGE 66 //


Then, outside the `my_game` instance, still in your game source file, define your own _talk_ verb alternative; for example:

[source,alan]
--------------------------------------------------------------------------------
SYNTAX my_talk_to = talk (act)
  WHERE act ISA ACTOR
    ELSE ...

SYNONYMS t = talk.

VERB my_talk_to
  DOES
    IF act = mr_smith
      THEN...
      ELSIF...
END VERB.
--------------------------------------------------------------------------------



[[sec.adding-checks]]
== Adding CHECKs


[WARNING]
================
@CHECK ME: See <<warn.my_game-verbs,the above warning>> regarding verb-overriding on the `my_game` instance.
================


Sometimes you might need to add an additional `CHECK` to a library-defined verb.
Add the check to the verb under the `my_game` instance and not in the library file.

[source,alan]
--------------------------------------------------------------------------------
THE soup ISA OBJECT AT kitchen
  IS edible.
  IS NOT hot.

  VERB eat
    CHECK soup IS hot
      ELSE "You must warm the soup first."
  END VERB.
END THE soup.
--------------------------------------------------------------------------------

Note that there is no `DOES ONLY` part in the above example.
The default outcome for the `eat` verb would be carried out on the soup only if the added `CHECK` passes.

// PAGE 67 //


[[sec.removing-checks]]
== Removing CHECKs

This requires accessing the library.
Open {lib_verbs}, find the verb you wish to remove a check from, and remove the target `CHECK` block.

WARNING: Make sure that after removing some default CHECKs the behavior of things in your game still remains coherent; after all, the default library verbs' checks are there to ensure that everything functions in a reasonable and consistent way.


[[sec.overriding-responses]]
== Overriding Responses

[WARNING]
================
@CHECK ME: See <<warn.my_game-verbs,the above warning>> regarding verb-overriding on the `my_game` instance.
================


Define the verb outcome with a `DOES ONLY` section within the `my_game` instance:

[source,alan]
--------------------------------------------------------------------------------
THE my_game ISA DEFINITION_BLOCK
  VERB examine
    DOES ONLY "Nothing special."
  END VERB.
END THE.
--------------------------------------------------------------------------------



[[sec.custom-verbs]]
== Custom Verbs

Declare a new verb in the normal manner instructed in the ALAN manual, outside any instances.

To create a verb that works globally and doesn't apply to any objects or actors:

[source,alan]
--------------------------------------------------------------------------------
SYNTAX test = test.

VERB test
  DOES "Test successful."
END VERB.
--------------------------------------------------------------------------------

// PAGE 68 //



Here is an example of creating a verb that applies to all objects in the game:

[source,alan]
--------------------------------------------------------------------------------
SYNTAX test = test (obj)
  WHERE obj ISA OBJECT
    ELSE "That's not something you can test."

ADD TO EVERY OBJECT
  VERB test
    DOES "You test" SAY THE obj. "successfully."
  END VERB.
END ADD.
--------------------------------------------------------------------------------



[[sec.restricted-actions]]
== Restricted Actions

Usually, when you need to restrict a verb from doing what it usually does (= when you want to change the default outcome as defined by the library), you can use a DOES ONLY statement:

[source,alan]
--------------------------------------------------------------------------------
THE book ISA OBJECT IN table
  DESCRIPTION ""

  VERB examine
    DOES ONLY "It's a thick, heavy book with leather covers."
  END VERB.
END THE book.
--------------------------------------------------------------------------------

(Using DOES ONLY here prevents the default examine response "You notice nothing unusual about the book." from being shown.)

or

[source,alan]
--------------------------------------------------------------------------------
THE basement ISA ROOM
  DESCRIPTION "..."

  VERB jump
    DOES ONLY "The ceiling is too low here."
  END VERB.
END THE basement.
--------------------------------------------------------------------------------

// PAGE 69 //

(The DOES ONLY here prevents the default message for jump, "You jump on the spot, to no avail." from being shown.)

// @FIXME: XREF TO PAGE

However, there are certain situations where you might wish to restrict the outcome for several verbs at once.
Let's imagine the hero is tied into a chair and cannot move his arms or legs.
Then, actions like examine, listen or think might still work, but actions like attack, eat and take should not be allowed to work.
For these situations, the library offers a way to restrict several verbs at once.
Look at the list of all library-defined verbs on p. 61-.
Now, there is a library-defined attribute for each and every verb -- CAN [verb].

If you want to disable any action or actions from the start of a game, you can declare for example

[source,alan]
--------------------------------------------------------------------------------
THE my_game ISA DEFINITION_BLOCK
  CAN NOT jump.
  CAN NOT dance.
  CAN NOT sing.
END THE my_game.
--------------------------------------------------------------------------------

and it won't be possible to jump, dance or sing in the game.
The above is a shorter way to disable verbs than

[source,alan]
--------------------------------------------------------------------------------
THE my_game ISA DEFINITION_BLOCK
  VERB jump
    DOES ONLY "You can't do that."
  END VERB.

  VERB dance
    DOES ONLY "You can't do that."
  END VERB.

  VERB sing
    DOES ONLY "You can't do that."
  END VERB.
END THE my_game.
--------------------------------------------------------------------------------

A list of all such attributes, corresponding to all implemented library verbs and commands, would start like this:

// @TODO: Check why 'again' is stropped? shouldn't need to be!

[source,alan]
--------------------------------------------------------------------------------
CAN about.
CAN 'again'.
CAN answer.
CAN ask.
CAN ask_for.
CAN attack.
--------------------------------------------------------------------------------

// PAGE 70 //



Notice how this list corresponds to the list of verbs on pp. 61-65, so it is not repeated fully here.

The outcome message for restricted verbs like the above is defined by the instance.
The default message is "You can't do that." but it can be easily edited:

[source,alan]
--------------------------------------------------------------------------------
THE my_game ISA DEFINITION_BLOCK
  HAS restricted_response "That's not possible presently.".
END THE my_game.
--------------------------------------------------------------------------------

or

[source,alan]
--------------------------------------------------------------------------------
THE my_game ISA DEFINITION_BLOCK
  HAS restricted_response "But you're tied up!".
END THE my_game.
--------------------------------------------------------------------------------

and so on.
Now, let's again think about the situation where the hero is tied into a chair and cannot move.
This kind of situation requires disabling a rather large number of verbs: attack, eat, take, drop, throw, put, along with numerous other ones.
One could do it like this:


[source,alan]
--------------------------------------------------------------------------------
EVENT tied_up
  "Suddenly you're interrupted. A couple of crooks enter the room,
   grab hold of you, push you into a chair, gag you and tie you
   into it tightly. You cannot move your arms or legs."
  MAKE my_game NOT attack.
  MAKE my_game NOT attack_with.
  MAKE my_game NOT bite.
  MAKE my_game NOT break.
  MAKE my_game NOT burn.
  MAKE my_game NOT burn_with.
  ...
END EVENT.
--------------------------------------------------------------------------------

but we quickly understand that such a list would grow very long.
That's why the library offers the option of disabling groups of verbs at once, through a specific attribute of the my_game instance: HAS restricted_level, which by default is 0.
Thus the following coding would actually be unnecessary, but it is included here anyway to show the needed formulation for this attribute:

// PAGE 71 //



[source,alan]
--------------------------------------------------------------------------------
THE my_game ISA DEFINITION_BLOCK
  HAS restricted_level 0.
END THE my_game.
--------------------------------------------------------------------------------

To change the level of restriction, do for example like this:

[source,alan]
--------------------------------------------------------------------------------
SET restricted_level OF my_game TO 2.
--------------------------------------------------------------------------------

The values of this attributes work in the following way:

a. `HAS restricted_level 0.`
+
This is the default value and it means that no verbs at all are restricted.
Everything works in the normal way.

b. `HAS restricted_level 1.`
+
This restriction can be used when the hero of the game is for example gagged, or the hero is an animal or other instance that cannot talk.
+
_Disabled actions_: `answer`, `ask`, `ask_for`, `say`, `say_to`, `shout`, `sing`, `tell`.
+
Please note that the verb sing is disabled in this group, as well.
Note also that communication verbs are automatically disabled when the restricted_level is 2, as well.

c. `HAS restricted_level 2.`
+
Here, verbs requiring physical action are disabled.
This would be the choice to take when you want to disable verbs when the hero is for example tied up into a chair, or under scrutiny, or in a situation where it would be awkward to try anything drawing attention, like when listening to a lecture, or hiding.
All action verbs, like attack, take, drop, eat, throw, put, etc. are disabled.
All communication verbs, like ask, say and tell are disabled, as well.
Sensory verbs and "passive" action verbs like look, examine, smell, listen, think and wait work.
+
_Allowed actions_: `about`, `again`, `credits`, `examine`, `hint`, `inventory`, `listen0`, `listen`, `look`, `look_at`, `look_behind`, `look_in`, `look_out_of`, `look_through`, `look_under`, `look_up`, `no`, `notify`, `notify_off`, `notify_on`, `pray`, `quit`, `restart`, `restore`, `save`, `score`, `script`, `script_off`, `script_on`, `smell0`, `smell`, `think`, `think_about`, `wait`, `what_am_i`, `what_is`, `where_am_i`, `where_is`, `who_am_i`, `who_is`, `yes`.
+
If you anyway want an individual action verb to work additionally, you can for example do like this:
+
[source,alan]
--------------------------------------------------------------------------------
EVENT tied_up
  "Suddenly your investigations are interrupted.
   A couple of crooks enter the room, grab hold of you, push
   you sitting on a chair and tie you into it tightly.
   You cannot move your arms or legs."
  SET restricted_level OF my_game TO 2. -- all action verbs will be disabled
  MAKE my_game rub.                     -- but 'rub' will work
END EVENT.
--------------------------------------------------------------------------------
+
Then, you can for example examine, look, listen, wait etc. but also [.play]#&gt; _rub the strings together_# to make them loosen and open.
+
If you wish to enable communication verbs while you're tied up, you'll have to enable them individually with the "CAN [verb]" method.

d. `HAS restricted_level 3.`
+
Here, even the sensory verbs and "passive" action verbs allowed at the previous level are disabled, besides all physical action verbs.
In fact, all in-game verbs are disabled.
You can't even look or examine.
You can use this restriction level when you want to for example ignore what the player typed and bring the story forward nevertheless.
Only meta verbs like save, quit, restore and about work.
+
_Allowed actions_: `about`, `again`, `credits`, `hint`, `no`, `notify`, `notify_off`, `notify_on`, `quit`, `restart`, `restore`, `save`, `score`, `script`, `script_off`, `script_on`, `yes`.
+
Let's say that you might wish to make a game where only the look, examine and use verbs work.
+
Then, you should code
+
[source,alan]
--------------------------------------------------------------------------------
THE my_game ISA DEFINITION_BLOCK
  HAS restricted_level 3.
  CAN 'look'.
  CAN examine.
  CAN 'use'.
  CAN use_with.
END THE.
--------------------------------------------------------------------------------

e. `HAS restricted_level 4.`
+
At this level, all possible verbs, even meta verbs like save, quit, restore and about are disabled.
It is not usually recommended to use this strict disabling of verbs, but this option is nevertheless offered for some special circumstances. (And you can always allow a verb or two with the CAN [verb] attribute.)
+
_Allowed actions_: none.
+
This level of restriction comes in handy mostly in situations where you want to the game to ask the player about something that has only limited alternative replies, for example
+
[example,role="gametranscript"]
================================================================================
Do you want to restore a saved game (yes/no?)
&gt;
================================================================================
+
To only allow yes and no to work above, do like this:
(Let's imagine the question above is presented at the start of the game, before anything else happens.)
+
[source,alan]
--------------------------------------------------------------------------------
THE my_game ISA DEFINITION_BLOCK
  HAS restricted_level 4. -- all possible verbs disabled
  CAN yes. CAN 'no'.      -- but 'yes' and 'no' work
  HAS restricted_response "Please answer 'yes' or 'no'."
END THE.

THE restore_room ISA LOCATION
  NAME -- no name defined for this room
  DESCRIPTION "Do you want to restore a saved game (yes/no?)"

  VERB yes
    DOES ONLY
      SET restricted_level OF my_game TO 0.
      RESTORE.
  END VERB.

  VERB 'no'
    DOES ONLY
      SET restricted_level OF my_game TO 0.
      LOCATE hero AT room1.
  END VERB.
END THE.

THE room1 ISA LOCATION
  DESCRIPTION "This is the first room of the game."
END THE.

START AT restore_room.
--------------------------------------------------------------------------------
+
Let's say for example that you want to implement the Loud Room from Zork 1.
There, anything you type is repeated:
+
[example,role="gametranscript"]
================================================================================
&gt; _x me_ +
x x...

&gt; _take key_ +
take take...

&gt; _help_ +
help help...

&gt; _quit_ +
quit quit...
================================================================================
+
You can achieve this by implementing
+
[source,alan]
--------------------------------------------------------------------------------
THE loud_room ISA ROOM
  ENTERED
    SET restricted_level OF my_game TO 4.
    SET restricted_message OF my_game TO "\$v \$v...".
END THE.
--------------------------------------------------------------------------------
+
There are a couple of important things to remember with this restriction level.
Firstly, the exits (north, east, etc.) can not be disabled through these attributes.
You must edit the exit messages manually for each situation or location where you restrict the allowed actions.
+
[source,alan]
--------------------------------------------------------------------------------
THE loud_room ISA ROOM
  IS loud.
  EXIT east TO corridor
    CHECK loud_room IS NOT loud
      ELSE "east east..."
  END EXIT.
END THE.
--------------------------------------------------------------------------------
+
Secondly, runtime messages are triggered in the normal way (for example "You can't see any such thing.") and if you want to also disable them in one way or another, you have to edit the messages in the lib_messages file.
For example, to achieve the Loud Room effect above:
+
[source,alan]
--------------------------------------------------------------------------------
MESSAGE NO_SUCH:
  IF restricted_level OF my_game = 3
    THEN "\$v \$v..."
    ELSE "You can't see any such thing."
END IF.
--------------------------------------------------------------------------------
+
and the same applies to all other messages that might come into question.
+
NOTE: If you conjure up any verbs of your own and wish to disable them at some point in the game, you should add a corresponding attribute to the my_game instance and make it negative at the appropriate point.
Here is an example with the verb 'drive' which is not included in the library by default:
+
[source,alan]
--------------------------------------------------------------------------------
THE my_game ISA DEFINITION_BLOCK
  CAN drive.
END THE.

EVENT tied_up
  "One of the thugs ties you tightly into a chair,
   and you cannot move your arms or legs at all."
  SET restricted_level OF my_game TO 2.
  MAKE my_game NOT drive. -- 'drive' being a verb you have defined
END EVENT.
--------------------------------------------------------------------------------
+
You should also remember to make any self-implemented verb to work again after the restriction doesn't apply any longer.
+
If you have defined a lot of verbs of your own in a game, you can do like this:
+
First, declare the "CAN [verb]" attributes for your own verbs:
+
[source,alan]
--------------------------------------------------------------------------------
THE my_game ISA DEFINITION_BLOCK
  CAN drive.
  CAN recall.
  CAN ride.
  CAN type.
END THE.
--------------------------------------------------------------------------------
+
Then, define when they will be restricted:
+
[source,alan]
--------------------------------------------------------------------------------
WHEN restricted_level OF my_game > 1
  -- three of the above are action verbs, so we restrict them
  -- when the restricted_level is 2 or higher
  THEN
    MAKE my_game NOT drive.
    MAKE my_game NOT ride.
    MAKE my_game NOT type.

WHEN restricted_level OF my_game > 2
  THEN
    MAKE my_game NOT recall.
    -- recall is similar to examine, think, listen, etc.
    -- so we'll cancel it together with those verbs
    -- only (level 3 and higher)
--------------------------------------------------------------------------------
+
To make these verbs work again, define:
+
[source,alan]
--------------------------------------------------------------------------------
MAKE my_game drive.
MAKE my_game recall.
MAKE my_game ride.
MAKE my_game type.
--------------------------------------------------------------------------------
+
etc.


// EOF //
