////
********************************************************************************
*                                                                              *
*                     ALAN Standard Library User's Manual                      *
*                                                                              *
*                                  Chapter 8                                   *
*                                                                              *
********************************************************************************
////


[[ch8]]
== Using verbs and commands

The library defines numerous verbs that can be readily used in-game with any objects or actors.
For example, if you implement

[source,alan]
--------------------------------------------------------------------------------
THE ball ISA OBJECT AT garden
END THE ball.
--------------------------------------------------------------------------------

the library enables you to [.play]#&gt; _examine ball_#, [.play]#&gt; _take ball_#, [.play]#&gt; _throw ball_#, [.play]#&gt; _kick ball_#, etc.
(with more or less successful default outcomes, depending on the verb) without your having to code anything specific to make this possible.

=== Verb syntaxes used in the standard library

The following verbs and commands work automatically in a game that has been programmed using the library.
The synonyms and built-in syntaxes are listed in connection with each verb.
(The syntaxes show the form of the command the player must type in-game to use each verb so that the game understands it: )

// ***********
// VERBS TABLE
// ***********
// Included as DSV (Delimiter-Separated Values) file.

// @TODO: Both this table and the one in "lib_verbs.i" should be checked again
//        to ensure they are correct. We should also find a way to keep them in
//        synch, maybe via SED; the problem is that the table in "lib_verbs.i"
//        has more columns than this one, so maybe an intermediate source table
//        would required, from which to extract this one via SED.

[cols="m,d,d",options="header",format=dsv]
|===============================================================================
include::table_verbs.dsv[]
|===============================================================================


To see the outcomes for these verbs and commands, check either the file `lib_verbs.i` or `mygame_import.i` where you'll find a list of all verb outcomes.
The syntaxes of these verbs are defined in the library file `lib_verbs.i`.

Note that the `exit` and enter `verbs` won't have successful outcomes by default; after all, it is impossible to place an ACTOR (like the hero) inside a container, at least in the current version of ALAN.
To make for example the command [.play]#&gt; _enter car_# work, you should make the car a separate LOCATION and then locate the hero there at the DOES ONLY part of the enter verb in the car instance.
In other words, simulate entering and exiting by locating the hero in between locations:

[source,alan]
--------------------------------------------------------------------------------
THE driveway ISA LOCATION
  DESCRIPTION "Your house is to the north. You car is here. The street
               is to the south."
END THE.

THE inside_car ISA LOCATION AT driveway
END THE driveway.

THE car ISA OBJECT
  VERB enter
    CHECK hero NOT inside_car
      ELSE "You're inside the car already!"
    DOES ONLY
      LOCATE hero AT inside_car.
  END VERB.
END THE car.
--------------------------------------------------------------------------------



=== The philosophy used in deciding successful and unsuccessful outcomes for action in the library verbs

If you try the various actions in-game, with the library imported, you will notice that some actions are successful and result in what the player commanded, while other actions do nothing (= the action is unsuccessful).
For example the response to [.play]#&gt; _drop [object]_# will be [.play]#Dropped.#, the carried object being rejected from the hero's inventory and ending up in the location, while the response to [.play]#&gt; _attack [thing]_# is [.play]#Resorting to brute force is not the solution here.#.
Which actions are allowed to succeed and which are not is based on what is the most reasonable and expected outcome for the action -- the outcome that the game author most unlikely needs to edit except for special circumstances.
Please experiment with different verbs in-game to see whether the default outcome of a particular action is suitable for your game -- otherwise redefine the outcome of the verb in the my_game instance.
How this is done is described in more detail further below.

// PAGE 65 //



=== Adding alternative syntaxes for library verbs

If you wish to add flexibility to your game by allowing alternative syntaxes for certain verbs, you can do that easily in your own game source file.
Let's say that you want to for example change the syntax of the talk_to verb.
Elsewhere in this manual you'll find all verb syntaxes listed.
From there, you'll find out that the syntax of the talk verb is

[source,alan]
--------------------------------------------------------------------------------
talk_to = talk 'to' (act).
--------------------------------------------------------------------------------

enabling commands like

[example,role="gametranscript"]
================================================================================
&gt; _talk to man_
================================================================================


Let's imagine that you want to change this so that it's possible for the player to type

[example,role="gametranscript"]
================================================================================
&gt; _talk man_
================================================================================


or just
[example,role="gametranscript"]
================================================================================
&gt; _t man_
================================================================================


in other words, stating the character with whom you wish to talk, after the verb, without the preposition 'to'.
The easiest way to allow this is just to add an additional syntax for 'talk_to' in your own game file:

[source,alan]
--------------------------------------------------------------------------------
SYNTAX  talk_to = talk (act).
        talk_to = t (act).
--------------------------------------------------------------------------------

This syntax declaration should be outside the `my_game` instance, in your own game file.
This syntax declaration won't cancel the original syntax for 'talk_to' defined in the library; it would still be possible for the player to type [.play]#&gt; _talk to man_#, as well.

If you wish to cancel the original syntax altogether, do like this in your own game file:

[source,alan]
--------------------------------------------------------------------------------
THE my_game ISA DEFINITION BLOCK
  VERB talk_to
    DOES ONLY "To talk to someone, type ""talk [person]""
               or just ""t [person]""."
  END VERB.
END THE my_game.
--------------------------------------------------------------------------------

// PAGE 66 //


Then, outside the my_game instance, still in your own game source file, define your own talk verb, for example:

[source,alan]
--------------------------------------------------------------------------------
SYNTAX my_talk_to = talk (act)
  WHERE act ISA ACTOR
    ELSE ...

VERB my_talk_to
  DOES
    IF act = mr_smith
      THEN...
      ELSIF...
END VERB.

SYNONYMS t = talk.
--------------------------------------------------------------------------------

// @FIXME:  In the original PDF the following is an ordered list element starting
//          from (b), but there wasn't any ongoing list here!

a. Accessing the library:
+
Find the verb in the library file 'lib_verbs.i' and make the desired changes to the syntax.
(If you add or change a parameter, make sure that the verb checks function properly.)





=== Adding your own checks for library verbs

Sometimes you might need to add an additional check to a library-defined verb.
Add the check to the verb under the `my_game` instance and not in the library file.

[source,alan]
--------------------------------------------------------------------------------
THE soup ISA OBJECT AT kitchen
  IS edible.
  IS NOT hot.

  VERB eat
    CHECK soup IS hot
      ELSE "You must warm the soup first."
  END VERB.
END THE soup.
--------------------------------------------------------------------------------

Note that there is no DOES ONLY part above.
The default outcome for the verb take would be carried out if the check was passed.

// PAGE 67 //



=== Removing checks from library verbs

This requires accessing the library.
Go to `lib_verbs.i`, find the verb you wish to remove a check from and remove the check. (Make sure the behavior of things in your game remains sensible; the library verb checks, after all, are there to ensure that everything functions in a reasonable and rational way.)


=== Overriding default responses for library verbs

Define the verb outcome with a DOES ONLY section within the my_game instance:

[source,alan]
--------------------------------------------------------------------------------
THE my_game ISA DEFINITION_BLOCK
  VERB examine
    DOES ONLY "Nothing special."
  END VERB.
END THE.
--------------------------------------------------------------------------------


=== Making your own verbs

Declare a new verb in the normal manner instructed in the ALAN manual, outside any instances.

To create a verb that works globally and doesn't apply to any objects or actors:

[source,alan]
--------------------------------------------------------------------------------
SYNTAX test = test.

VERB test
  DOES "Test successful."
END VERB.
--------------------------------------------------------------------------------

// PAGE 68 //



Here is an example of creating a verb that applies to all objects in the game:

[source,alan]
--------------------------------------------------------------------------------
SYNTAX test = test (obj)
  WHERE obj ISA OBJECT
    ELSE "That's not something you can test."

ADD TO EVERY OBJECT
  VERB test
    DOES "You test" SAY THE obj. "successfully."
  END VERB.
END ADD.
--------------------------------------------------------------------------------


=== Restricted actions

Usually, when you need to restrict a verb from doing what it usually does (= when you want to change the default outcome as defined by the library), you can use a DOES ONLY statement:

[source,alan]
--------------------------------------------------------------------------------
THE book ISA OBJECT IN table
  DESCRIPTION ""

  VERB examine
    DOES ONLY "It's a thick, heavy book with leather covers."
  END VERB.
END THE book.
--------------------------------------------------------------------------------

(Using DOES ONLY here prevents the default examine response "You notice nothing unusual about the book." from being shown.)

or

[source,alan]
--------------------------------------------------------------------------------
THE basement ISA ROOM
  DESCRIPTION "..."

  VERB jump
    DOES ONLY "The ceiling is too low here."
  END VERB.
END THE basement.
--------------------------------------------------------------------------------

// PAGE 69 //

(The DOES ONLY here prevents the default message for jump, "You jump on the spot, to no avail." from being shown.)

// @FIXME: XREF TO PAGE

However, there are certain situations where you might wish to restrict the outcome for several verbs at once.
Let's imagine the hero is tied into a chair and cannot move his arms or legs.
Then, actions like examine, listen or think might still work, but actions like attack, eat and take should not be allowed to work.
For these situations, the library offers a way to restrict several verbs at once.
Look at the list of all library-defined verbs on p. 61-.
Now, there is a library-defined attribute for each and every verb -- CAN [verb].

If you want to disable any action or actions from the start of a game, you can declare for example

[source,alan]
--------------------------------------------------------------------------------
THE my_game ISA DEFINITION_BLOCK
  CAN NOT jump.
  CAN NOT dance.
  CAN NOT sing.
END THE my_game.
--------------------------------------------------------------------------------

and it won't be possible to jump, dance or sing in the game.
The above is a shorter way to disable verbs than

[source,alan]
--------------------------------------------------------------------------------
THE my_game ISA DEFINITION_BLOCK
  VERB jump
    DOES ONLY "You can't do that."
  END VERB.

  VERB dance
    DOES ONLY "You can't do that."
  END VERB.

  VERB sing
    DOES ONLY "You can't do that."
  END VERB.
END THE my_game.
--------------------------------------------------------------------------------

A list of all such attributes, corresponding to all implemented library verbs and commands, would start like this:

// @TODO: Check why 'again' is stropped? shouldn't need to be!

[source,alan]
--------------------------------------------------------------------------------
CAN about.
CAN 'again'.
CAN answer.
CAN ask.
CAN ask_for.
CAN attack.
--------------------------------------------------------------------------------

// PAGE 70 //



Notice how this list corresponds to the list of verbs on pp. 61-65, so it is not repeated fully here.

The outcome message for restricted verbs like the above is defined by the instance.
The default message is "You can't do that." but it can be easily edited:

[source,alan]
--------------------------------------------------------------------------------
THE my_game ISA DEFINITION_BLOCK
  HAS restricted_response "That's not possible presently.".
END THE my_game.
--------------------------------------------------------------------------------

or

[source,alan]
--------------------------------------------------------------------------------
THE my_game ISA DEFINITION_BLOCK
  HAS restricted_response "But you're tied up!".
END THE my_game.
--------------------------------------------------------------------------------

and so on.
Now, let's again think about the situation where the hero is tied into a chair and cannot move.
This kind of situation requires disabling a rather large number of verbs: attack, eat, take, drop, throw, put, along with numerous other ones.
One could do it like this:


[source,alan]
--------------------------------------------------------------------------------
EVENT tied_up
  "Suddenly you're interrupted. A couple of crooks enter the room,
   grab hold of you, push you into a chair, gag you and tie you
   into it tightly. You cannot move your arms or legs."
  MAKE my_game NOT attack.
  MAKE my_game NOT attack_with.
  MAKE my_game NOT bite.
  MAKE my_game NOT break.
  MAKE my_game NOT burn.
  MAKE my_game NOT burn_with.
  ...
END EVENT.
--------------------------------------------------------------------------------

but we quickly understand that such a list would grow very long.
That's why the library offers the option of disabling groups of verbs at once, through a specific attribute of the my_game instance: HAS restricted_level, which by default is 0.
Thus the following coding would actually be unnecessary, but it is included here anyway to show the needed formulation for this attribute:

// PAGE 71 //



[source,alan]
--------------------------------------------------------------------------------
THE my_game ISA DEFINITION_BLOCK
  HAS restricted_level 0.
END THE my_game.
--------------------------------------------------------------------------------

To change the level of restriction, do for example like this:

[source,alan]
--------------------------------------------------------------------------------
SET restricted_level OF my_game TO 2.
--------------------------------------------------------------------------------

The values of this attributes work in the following way:

a. `HAS restricted_level 0.`
+
This is the default value and it means that no verbs at all are restricted.
Everything works in the normal way.

b. `HAS restricted_level 1.`
+
This restriction can be used when the hero of the game is for example gagged, or the hero is an animal or other instance that cannot talk.
+
_Disabled actions_: `answer`, `ask`, `ask_for`, `say`, `say_to`, `shout`, `sing`, `tell`.
+
Please note that the verb sing is disabled in this group, as well.
Note also that communication verbs are automatically disabled when the restricted_level is 2, as well.

c. `HAS restricted_level 2.`
+
Here, verbs requiring physical action are disabled.
This would be the choice to take when you want to disable verbs when the hero is for example tied up into a chair, or under scrutiny, or in a situation where it would be awkward to try anything drawing attention, like when listening to a lecture, or hiding.
All action verbs, like attack, take, drop, eat, throw, put, etc. are disabled.
All communication verbs, like ask, say and tell are disabled, as well.
Sensory verbs and "passive" action verbs like look, examine, smell, listen, think and wait work.
+
_Allowed actions_: `about`, `again`, `brief`, `credits`, `examine`, `hint`, `inventory`, `listen0`, `listen`, `look`, `look_at`, `look_behind`, `look_in`, `look_out_of`, `look_through`, `look_under`, `look_up`, `no`, `notify`, `notify_off`, `notify_on`, `pray`, `quit`, `restart`, `restore`, `save`, `score`, `script`, `script_off`, `script_on`, `smell0`, `smell`, `think`, `think_about`, `verbose`, `wait`, `what_am_i`, `what_is`, `where_am_i`, `where_is`, `who_am_i`, `who_is`, `yes`.
+
If you anyway want an individual action verb to work additionally, you can for example do like this:
+
[source,alan]
--------------------------------------------------------------------------------
EVENT tied_up
  "Suddenly your investigations are interrupted.
   A couple of crooks enter the room, grab hold of you, push
   you sitting on a chair and tie you into it tightly.
   You cannot move your arms or legs."
  SET restricted_level OF my_game TO 2. -- all action verbs will be disabled
  MAKE my_game rub.                     -- but 'rub' will work
END EVENT.
--------------------------------------------------------------------------------
+
Then, you can for example examine, look, listen, wait etc. but also [.play]#&gt; _rub the strings together_# to make them loosen and open.
+
If you wish to enable communication verbs while you're tied up, you'll have to enable them individually with the "CAN [verb]" method.

d. `HAS restricted_level 3.`
+
Here, even the sensory verbs and "passive" action verbs allowed at the previous level are disabled, besides all physical action verbs.
In fact, all in-game verbs are disabled.
You can't even look or examine.
You can use this restriction level when you want to for example ignore what the player typed and bring the story forward nevertheless.
Only meta verbs like save, quit, restore and about work.
+
_Allowed actions_: `about`, `again`, `brief`, `credits`, `hint`, `no`, `notify`, `notify_off`, `notify_on`, `quit`, `restart`, `restore`, `save`, `score`, `script`, `script_off`, `script_on`, `verbose`, `yes`.
+
Let's say that you might wish to make a game where only the look, examine and use verbs work.
+
Then, you should code
+
[source,alan]
--------------------------------------------------------------------------------
THE my_game ISA DEFINITION_BLOCK
  HAS restricted_level 3.
  CAN 'look'.
  CAN examine.
  CAN 'use'.
  CAN use_with.
END THE.
--------------------------------------------------------------------------------

e. `HAS restricted_level 4.`
+
At this level, all possible verbs, even meta verbs like save, quit, restore and about are disabled.
It is not usually recommended to use this strict disabling of verbs, but this option is nevertheless offered for some special circumstances. (And you can always allow a verb or two with the CAN [verb] attribute.)
+
_Allowed actions_: none.
+
This level of restriction comes in handy mostly in situations where you want to the game to ask the player about something that has only limited alternative replies, for example
+
[example,role="gametranscript"]
================================================================================
Do you want to restore a saved game (yes/no?)
&gt;
================================================================================
+
To only allow yes and no to work above, do like this:
(Let's imagine the question above is presented at the start of the game, before anything else happens.)
+
[source,alan]
--------------------------------------------------------------------------------
THE my_game ISA DEFINITION_BLOCK
  HAS restricted_level 4. -- all possible verbs disabled
  CAN yes. CAN 'no'.      -- but 'yes' and 'no' work
  HAS restricted_response "Please answer 'yes' or 'no'."
END THE.

THE restore_room ISA LOCATION
  NAME -- no name defined for this room
  DESCRIPTION "Do you want to restore a saved game (yes/no?)"

  VERB yes
    DOES ONLY
      SET restricted_level OF my_game TO 0.
      RESTORE.
  END VERB.

  VERB 'no'
    DOES ONLY
      SET restricted_level OF my_game TO 0.
      LOCATE hero AT room1.
  END VERB.
END THE.

THE room1 ISA LOCATION
  DESCRIPTION "This is the first room of the game."
END THE.

START AT restore_room.
--------------------------------------------------------------------------------
+
Let's say for example that you want to implement the Loud Room from Zork 1.
There, anything you type is repeated:
+
[example,role="gametranscript"]
================================================================================
&gt; _x me_ +
x x...

&gt; _take key_ +
take take...

&gt; _help_ +
help help...

&gt; _quit_ +
quit quit...
================================================================================
+
You can achieve this by implementing
+
[source,alan]
--------------------------------------------------------------------------------
THE loud_room ISA ROOM
  ENTERED
    SET restricted_level OF my_game TO 4.
    SET restricted_message OF my_game TO "\$v \$v...".
END THE.
--------------------------------------------------------------------------------
+
There are a couple of important things to remember with this restriction level.
Firstly, the exits (north, east, etc.) can not be disabled through these attributes.
You must edit the exit messages manually for each situation or location where you restrict the allowed actions.
+
[source,alan]
--------------------------------------------------------------------------------
THE loud_room ISA ROOM
  IS loud.
  EXIT east TO corridor
    CHECK loud_room IS NOT loud
      ELSE "east east..."
  END EXIT.
END THE.
--------------------------------------------------------------------------------
+
Secondly, runtime messages are triggered in the normal way (for example "You can't see any such thing.") and if you want to also disable them in one way or another, you have to edit the messages in the lib_messages file.
For example, to achieve the Loud Room effect above:
+
[source,alan]
--------------------------------------------------------------------------------
MESSAGE NO_SUCH:
  IF restricted_level OF my_game = 3
    THEN "\$v \$v..."
    ELSE "You can't see any such thing."
END IF.
--------------------------------------------------------------------------------
+
and the same applies to all other messages that might come into question.
+
NOTE: If you conjure up any verbs of your own and wish to disable them at some point in the game, you should add a corresponding attribute to the my_game instance and make it negative at the appropriate point.
Here is an example with the verb 'drive' which is not included in the library by default:
+
[source,alan]
--------------------------------------------------------------------------------
THE my_game ISA DEFINITION_BLOCK
  CAN drive.
END THE.

EVENT tied_up
  "One of the thugs ties you tightly into a chair,
   and you cannot move your arms or legs at all."
  SET restricted_level OF my_game TO 2.
  MAKE my_game NOT drive. -- 'drive' being a verb you have defined
END EVENT.
--------------------------------------------------------------------------------
+
You should also remember to make any self-implemented verb to work again after the restriction doesn't apply any longer.
+
If you have defined a lot of verbs of your own in a game, you can do like this:
+
First, declare the "CAN [verb]" attributes for your own verbs:
+
[source,alan]
--------------------------------------------------------------------------------
THE my_game ISA DEFINITION_BLOCK
  CAN drive.
  CAN recall.
  CAN ride.
  CAN type.
END THE.
--------------------------------------------------------------------------------
+
Then, define when they will be restricted:
+
[source,alan]
--------------------------------------------------------------------------------
WHEN restricted_level OF my_game > 1
  -- three of the above are action verbs, so we restrict them
  -- when the restricted_level is 2 or higher
  THEN
    MAKE my_game NOT drive.
    MAKE my_game NOT ride.
    MAKE my_game NOT type.

WHEN restricted_level OF my_game > 2
  THEN
    MAKE my_game NOT recall.
    -- recall is similar to examine, think, listen, etc.
    -- so we'll cancel it together with those verbs
    -- only (level 3 and higher)
--------------------------------------------------------------------------------
+
To make these verbs work again, define:
+
[source,alan]
--------------------------------------------------------------------------------
MAKE my_game drive.
MAKE my_game recall.
MAKE my_game ride.
MAKE my_game type.
--------------------------------------------------------------------------------
+
etc.


// EOF //
