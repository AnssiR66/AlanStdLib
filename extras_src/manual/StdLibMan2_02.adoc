////
********************************************************************************
*                                                                              *
*                     ALAN Standard Library User's Manual                      *
*                                                                              *
*                    PART II — Library Classes » Locations                     *
*                                                                              *
********************************************************************************
////


[[ch.locations]]
= Locations

Location classes pre-defined in the library:

* `ROOM`
* `SITE`
* `DARK_LOCATION`


Default location attributes pre-defined in the library:

* `IS lit.`
* `IS visited 0.`
* `IS described 0.`

(For specific attributes for ROOMs, SITEs and DARK_LOCATIONs, see below.)

Using the standard library, basic locations are implemented just like advised in the ALAN Manual, for example:

// @EXTERN CODE: _locations_no-lib.alan
[source,alan]
-------------------------------------------
include::{utf8dir}/_locations_no-lib.alan[]
-------------------------------------------



== Indoor and Outdoor

Since any medium- to large-adventure is likely to contain both indoor and outdoor locations, the library provides two useful predefined classes for the task:

[horizontal]
`ROOM` :: An indoor location equipped with walls, floor and ceiling.
`SITE` :: An outdoor location equipped with ground and sky.

These allow you to quickly create indoor and outdoor locations without having to worry about the details, providing a more realistic game experience.
The library also provides some default responses for when the player attempts to examine the predefined room- and site-objects (walls, ground, etc.), which you can easily override either globally or on a per room/site basis.

The `ROOM` and `SITE` classes are implemented as nested locations inside the `indoor` and `outdoor` locations, respectively, which are two predefined library instances acting as "`regions`" -- i.e. outward containers for every `ROOM` and `SITE` instance of an adventure.

The room objects (`walls`, `floor` and `ceiling`) are implemented on the `indoor` instance, which makes them available to every instance of the `ROOM` class, because of how scoping rules work with nested locations.
Likewise, site objects (`ground` and `sky`) are defined directly on the `outdoor` location instance, and "`inherited`" (so to speak) by every `SITE` instance.



=== ROOM

If you want to implement an indoor location, you can declare it `IsA ROOM`:

[source,alan]
--------------------------------------------------------------------------------
The kitchen IsA room
  Description "..."
  ...
End the kitchen.
--------------------------------------------------------------------------------

All ROOMs will automatically have walls, a floor and a ceiling.

There are three attributes for describing the walls, the floor and the ceiling in any ROOM:

// @FIXME: Use StdLib color scheme here!
[source,alan]
--------------------------------------------------------------------------------
HAS floor_desc "".
HAS walls_desc "".
HAS ceiling_desc "".
--------------------------------------------------------------------------------

If the author doesn't change these default values, the default description shown when examining the walls, floor and ceiling of a room will be [.play]#You notice nothing unusual about the [object].#
(Obviously, these commands won't work in outdoor locations.)

To have a more varied effect, you can change the descriptions this way:

// @EXTERN CODE: livingroom
[source,alan]
--------------------------------------------------------------------------------
include::{utf8dir}/livingroom.alan[tag=livingroom]
--------------------------------------------------------------------------------

and the player will be presented with your custom descriptions for the living room's foor, ceiling and walls:

// @EXTERN TRANSCRIPT: livingroom
[example,role="gametranscript"]
================================================================================
include::{utf8dir}/livingroom.a3ADocLog[tag=livingroom]
================================================================================


[NOTE]
================
Setting these room attributes in an `outdoor` location won't have any effect; they only work on instances of the `room` class, or its sub-classes, and will be ignored elsewhere.
================

You can even change the description of a room's floor, walls or ceiling mid-game at any time:

// @EXTERN CODE: livingroom -> trapdoor
[source,alan]
--------------------------------------------------------------------------------
include::{utf8dir}/livingroom.alan[tag=trapdoor]
--------------------------------------------------------------------------------


// @EXTERN TRANSCRIPT: livingroom -> trapdoor
[example,role="gametranscript"]
================================================================================
include::{utf8dir}/livingroom.a3ADocLog[tag=trapdoor]
================================================================================



=== SITE

If you want to implement an outdoor location, you can declare it `IsA SITE`:

// @EXTERNALIZE CODE: meadow.alan (implementing outdoor location)
[source,alan]
---------------------------------------------------
include::{utf8dir}/meadow.alan[tag=outdoor-example]
---------------------------------------------------

All SITEs will automatically have a ground and a sky.

There are two attributes for describing the ground and the sky in any SITE:


* `Has ground_desc "".`
* `Has sky_desc "".`

If the author doesn't change these default values, the default description, at [.play]#&gt; _x floor_#, [.play]#&gt; _x walls_# or [.play]#&gt; _x ceiling_# will be [.play]#You notice nothing unusual about the [object].#
(Obviously, these commands won't work in indoor locations.)


Again, you can change these messages at any point mid-game:

// @EXTERNALIZE CODE: meadow.alan (changing sit-objs descriptions mid-game)
[source,alan]
------------------------------------------------
include::{utf8dir}/meadow.alan[tag=change-descs]
------------------------------------------------

// @EXTERN TRANSCRIPT: XXX
[example,role="gametranscript"]
================================================================================
include::{utf8dir}/meadow.a3ADocLog[tag=change-descs]
================================================================================


[NOTE]
================================================================================
__NOTE 1__: To manipulate the floor, the walls and the ceiling (in indoor locations) and the ground and the sky (in outdoor locations), see the following example.
Here, the hero digs the ground on the meadow:

[source,alan]
--------------------------------------------------------------------------------
The meadow IsA site
  Description "Green grass, tall trees and the big sky above you.
               What could be more pleasant?"
  Has sky_desc "The sky is blue."
  Verb dig
    Does only
    If obj = ground
      Then "You dig the ground with your shovel and find a chest!"
        -- Here, you could change the description of the ground if you wish:
        --   Set ground_desc of meadow to "There's a hole in the ground, left
        --                                 there by your digging efforts.".
      Else "That doesn't work."
    End if.
  End verb.
End the.
--------------------------------------------------------------------------------


Notice the bit `If obj = ground` above.
The library-defined indoor and outdoor objects, besides the `ground`, are `sky`, `floor`, `walls` and `ceiling`.
You can refer to them in your coding in the way illustrated above when you need to manipulate them in any way.
================================================================================


[NOTE]
================================================================================
__NOTE 2__: Besides using the `floor_desc`, `walls_desc` etc attributes for the indoor and outdoor location objects, you can also do like in the following example.

[source,alan]
--------------------------------------------------------------------------------
The my_game IsA definition_block
  Verb examine
    Does only
    Check obj <> walls
      Else
        If hero at kitchen
          Then "The walls are lined with shelves."
        ElsIf hero at livingroom
          Then "The wallpaper has a nice flower pattern."
        ElsIf hero at ...
        End if.
    And obj <> floor
      Then ...
      ...
  End verb.
End the my_game.
--------------------------------------------------------------------------------
================================================================================


[[sec.dark-places]]
== Dark Places

[WARNING]
=====================================
The original section on ``DARK_LOCATION``s is a bit confusing; it will be much better if we add an introduction briefly explaining that the Library offers two possible ways of handling dark places: via unlit ROOMs, or using DARK_LOCATIONs.

The we should present first the "`Unlit Locations`" subsection, followed by the "`DARK_LOCATION`" section.

In it's current state, were the distinction between the two follows the DARK_LOCATION section, it's confusing because there's too much info at stake; whereas an overview intro would prepare the reader to pick-up better the subtle differences between the two.
Also, keeping the two approaches separate, each with its own examples, is going to offer a better context, and possibly stick more with the reader.
=====================================

When it comes to implementing dark locations, the library offers two different solutions:

1. Using an unlit location.

2. Using a `DARK_LOCATION` instance.

Both solutions have similarities, but they also differ in significant ways, so it's worth spending a few introductory words on the pros and cons of each approach.

The library adds to every location the `IS lit` attribute, except for the `DARK_LOCATION` class, which is set to `NOT lit` by default.
Therefore, any location can be turned dark (i.e. _unlit_) by clearing its `lit` attribute.

The library also ensures that, whenever the player's is in an unlit location of _any_ type, he/she won't be able to carry out any actions that require the presence of light, and instead of printing the location's description it will show the default `dark_loc_desc` message:

[example,role="gametranscript"]
=================================================
It is pitch black. You can't see anything at all.
=================================================

These are the commonalities between unlit locations and `DARK_LOCATION` instances.
Let's now look at their significant differences -- skipping the obvious and trivial fact that locations are lit by default, whereas `DARK_LOCATION` instances are unlit.

In order to be lit, a `DARK_LOCATION` requires the presence of a `lightsource` object (e.g. a candle, a torch, whatever).
The library constantly checks for the presence of `lightsource` objects in a `DARK_LOCATION`, and automatically sets or clears its `lit` attribute whenever a light source enters or leaves the location (e.g. not only via the hero, but also an entering or leaving NPC that is carrying a light source, or the light source materializing or disappearing by other means).

Unlike unlit locations, which have specific mechanisms for determining the transition from lit to unlit (e.g. a switch, a timer, etc.), a `DARK_LOCATION` can be lit by _any_ light source -- which has far-reaching implications on how an adventure might unfold.

To state it simply, the diffrence between the two approaches boils down to _who's in charge_ of the location's ligthing mechanism:

* With unlit locations, the author is in control of the location's illumination.
* With DARK_LOCATIONs, the player is in control of the location's illumination.

Setting or clearing the `lit` attribute of a given location is something that only the game author can do, by implementing some mechanism that flips the attribute -- be it the result of the player acting on a switch, an actor carrying out a specific action, or via an internal timer set by the game author, or some other EVENT or RULE mechanism.

On the other hand, the player is able to choose _if_ and _which_ light source to carry to the dark location in order light it up, as long as `lightsource` objects are available in the game world.

The two approaches have different implications on how the adventure might unfold for the player.
If the hero has dropped the only available light source on the opposite end of a huge map, he might have to go all the way back to fetch it in order to light a DARK_LOCATION -- providing an unforgiving game play experience.
If there are multiple light sources to be found in the game, anyone will do the job -- providing a flexible game play experience.

On the other hand, there are also implications on the author's side, especially when it comes to unpredicted consequences.
If the player destroys or renders otherwise unreachable all light sources, the adventure might become unwinnable.
Miscalculated access to light source objects across the map might break the puzzle sequence of a DARK_LOCATION, if the player manages to get hold of an unexpected light source.

Because unlit locations have a limited set of ways by which they can become lit, they provide authors with a more rigid (and reliable) way to control how the player can illuminate them.
On the other hand, the flexibility of DARK_LOCATIONs can lead to a more realistic and "`open world`" player experience (with all its pros and cons).



=== DARK_LOCATION and the lit attribute

In dark locations, all actions requiring the ability to see are automatically disabled by the library.
All dark locations have the `NOT lit` attribute.
In order for locations of the `DARK_LOCATION` subclass to be lit, they need the presence of a lit `LIGHTSOURCE` object.

Here's an example of how to implement a `DARK_LOCATION`:


[source,alan]
--------------------------------------------------------------------------------
The basement IsA dark_location
  Exit up to hall.
End the.
--------------------------------------------------------------------------------

// @FIXME: The 'dark_loc_desc' attribute is now in "lib_messages_library.i".
//         Also, update the page cross-ref with a real XRef.

The default description of a dark location will be "`It is pitch black.
You can't see anything at all.`"
This default description can be changed by editing the `dark_loc_desc` attribute of the `my_game` instance (see p. 78).

If you add your own description to a `DARK_LOCATION`, it will only be shown if the location is lit up by some means:


[source,alan]
--------------------------------------------------------------------------------
The basement IsA dark_location
  Description "Only useless junk can be seen lying around."
  Exit up to hall.
End the.
--------------------------------------------------------------------------------


In order for a `DARK_LOCATION` to be lit, a `LIGHTSOURCE` object (a lantern, a match, a ceiling lamp, or any other kind of object emitting light) needs to be present and lit.

In darkness, you are not able to manipulate things other than turn on a LIGHTSOURCE and drop items you're carrying (these checks are found in `lib_verbs.i`).
You can exit normally and use verbs that don't require seeing, such as smell, listen and think.
If you are in a DARK_LOCATION with an NPC (= a non-player character), you are able to communicate with them by asking and telling, but not by showing and giving.
If you wish to change these restrictions, see the respective verbs in `lib_verbs.i` and modify their checks.

Note that you cannot change the name of a location mid-game.
Therefore, if you name a dark location '`Darkness`' and wish to make it lit at some point in the game, its name will still be '`Darkness`' even if the location description can be changed to describe the illuminated location.
To show a change in the location name, you must locate the hero in another location when the dark location is lit.
For example,

[source,alan]
--------------------------------------------------------------------------------
The lantern IsA lightsource
  Verb turn_on
    Does
    If hero at darkness
      then locate hero at treasure_chamber.
    End if.
  End verb.
End the.
--------------------------------------------------------------------------------

Alternatively, you can also use a rule, for example


[source,alan]
--------------------------------------------------------------------------------
When lantern is lit
  and hero at darkness
then locate hero at treasure_chamber.
--------------------------------------------------------------------------------

Note that you won't always need to define a dark location to be a member of the subclass DARK_LOCATION.
This applies in cases when you don't wish to implement LIGHTSOURCE objects to make locations lit or not lit. (All location instances have by default the attribute lit and they can be made NOT lit when needed.) For example, suppose you want all dark locations in the game to become lighted simultaneously.
It can be done for example like this:

[source,alan]
--------------------------------------------------------------------------------
The main_power_switch IsA device at lobby
  Verb switch_on
    Does only
      For each dl isa location, is not lit
        do
        make dl lit.
      End each.
  ENd verb.
ENd the.
--------------------------------------------------------------------------------

If we had used the DARK_LOCATION class above, all locations to be lighted should have had a LIGHTSOURCE object present in them, and all these LIGHTSOURCE objects would have needed to be changed to lit, which would have meant extra programming.


Even normal locations, when not lit, will have the description "It is pitch black.
You can't see anything at all.", so you can use the above method with no worries.
The only reason for a specific DARK_LOCATION subclass to exist is to make it automatic for them to be lit or NOT lit when the hero is carrying around and/or turning on and off LIGHTSOURCES so that the game author won't constantly need to remember to change the attribute of the location to lit or NOT lit in all imaginable cases.

Also consider the following case: suppose the hero can make a basement (a location belonging to the class DARK_LOCATION) lighted by turning on a light switch that is at the top of the stairs leading to the basement (a different location from the basement itself).
We program the light switch object so that when the hero turns it on, the basement will be lit.
All ok so far.
However, when the hero enters the actual basement, it will be dark.
Why?
Because there is no LIGHTSOURCE present in the basement; we just changed the attribute of the basement location to lit, but this is not enough.
A check at entering any DARK_LOCATION will make the location dark if no lit LIGHTSOURCE is present.
You should program a lamp, a LIGHTSOURCE object, to be present in the basement, and this lamp should be made lit at the same time when the hero turns on the switch at the top of the stairs.
But again, this is more than is necessary to reach the wanted effect.
Here, like above, you could just make the basement a normal location and not a DARK_LOCATION (and make sure it is NOT lit to start with), and just change the attribute to lit when the hero turns on the light switch:

// @EXTERNALIZE CODE: basement.alan
[source,alan]
--------------------------------------------------------------------------------
include::{utf8dir}/basement.alan[tag=unlit-room-example]
--------------------------------------------------------------------------------

As you can see in the following transcript, while the basement location is `NOT lit` the player in unable to see the room's description or carry out any actions that require the presence of light:

// @EXTERN TRANSCRIPT: basement.alan
[example,role="gametranscript"]
================================================================================
include::{utf8dir}/basement.a3ADocLog[tag=unlit-room-example-1]
================================================================================

by return upstairs and turning on the light switch the basement becomes `lit`, and the room's normal behavior is restored:

// @EXTERN TRANSCRIPT: basement.alan
[example,role="gametranscript"]
================================================================================
include::{utf8dir}/basement.a3ADocLog[tag=unlit-room-example-2]
================================================================================


To recap: use the DARK_LOCATION class when a LIGHTSOURCE object determines whether a location is lit or dark; swap between the lit/NOT lit attributes, inherent to all locations, when you don't want to refer to any light sources affecting whether a location is lit or dark.


== The visited and described attributes


// @ADDED SUB-HEADING:
=== visited

[source,alan]
--------------------------------------------------------------------------------
IS visited 0.
--------------------------------------------------------------------------------

A location not visited at all has the `visited` value 0.
When the hero enters it the first time, the `visited` value will change to 1.
On the second visit the value will be 2, etc.

Now, in your source code you can define something like the following:

[source,alan]
--------------------------------------------------------------------------------
The kitchen IsA location
  Description
    "You are in the kitchen."
  If visited of this = 1
    then "This is your first time here."
    else "You remember you've been here before."
  End if.
End the.
--------------------------------------------------------------------------------

You can also check whether the hero has been in a LOCATION if needed:

[source,alan]
--------------------------------------------------------------------------------
The king IsA person
  Verb ask
    When act
    If topic = treasure_chamber
      then
        IF visited of treasure_chamber = 0
          then "You are not supposed to know anything
                about the treasure chamber - you
                haven't found it yet."
          else """Just take what you want from the
                chamber"", the king smiles."
        End if.
    End if.
  End verb.
End the.
--------------------------------------------------------------------------------



// @ADDED SUB-HEADING:
=== described

[source,alan]
--------------------------------------------------------------------------------
IS described 0.
--------------------------------------------------------------------------------

Suppose you want the location description to be different after the first time the description is shown, even if you are in the LOCATION still for the first time.
Then, you can use the `described` attribute.
A LOCATION not described at all has the described value 0.
When the player reads the location description for the first time, the value is 1, the next time the value will be 2, etc:

[source,alan]
--------------------------------------------------------------------------------
The library IsA room
  Description
    If described of this = 1
      then "There is an old man reading at a desk in one of the
            corners."
      else "The old man keeps on reading at his desk."
    End if.
End the.
--------------------------------------------------------------------------------

or:

[source,alan]
--------------------------------------------------------------------------------
The meadow IsA site
  Description
    "Flies and other insects buzz around you"
    If described OF meadow > 5
      then ", which starts to annoy you little by little"
    End if.
    "."
End the.
--------------------------------------------------------------------------------




== Changing the verb outcome in a certain location

Sometimes you might wish to have a verb behave differently in a certain location or locations.
You can do it like this:

// @EXTERNALIZE CODE: basement
[source,alan]
--------------------------------------------------------------------------------
The basement IsA location
  Description "This is the basement of your house. Stairs lead up."
  Exit up to livingroom.
  Verb jump
    Does only "The ceiling is too low here."
  End verb.
End the.
--------------------------------------------------------------------------------

or

// @EXTERNALIZE CODE: exhibition_hall
[source,alan]
--------------------------------------------------------------------------------
The exhibition_hall IsA room
  Description "You are in the main exhibition of the museum. There is
               exquisite art all around you."
  Verb take
    does only "Trying to take anything here just like that would
               set the alarm off immediately."
  End verb.
End the.
--------------------------------------------------------------------------------


In the first example, the response to the jump verb has been changed to fit the low basement better.
Notice in the second example that the verb
`take` doesn't apply to the location `exhibition_hall` even if it is listed within it ( = [.play]#&gt; _take hall_# won't be a successful action), but sooner to the objects found in that location.
Thus, if there was an exquisite vase in the exhibition hall and the hero tried to take it, the above message would be shown.
Verbs cannot refer to locations, they usually apply to things or numerals.
Thus, the above coding will result in:

// @EXTERNALIZE TRANSCRIPT: exhibition_hall -> vase
[example,role="gametranscript"]
================================================================================
&gt; _take vase_ +
Trying to take anything here just like that would set the alarm off +
immediately.
================================================================================


At times, you might wish to have the location-specific verb restriction lifted in certain situations.
Then, you can use a check in the verb within the location.
In the following example, the hero will be able to take the vase, or anything else, in the exhibition hall on the condition that an alarm device is turned off:

[source,alan]
--------------------------------------------------------------------------------
The exhibition_hall IsA room
  Description "You are in the main exhibition of the museum. There is
               exquisite art all around you."
  Verb take
    Check alarm is not 'on'
      else "Trying to take anything here just like that would
            set the alarm off immediately."
  End verb.
End the.
--------------------------------------------------------------------------------


Notice that there is no `DOES` section in the take verb above.
If the alarm is turned off, the take action would be successful, as defined by default in the library.
You would naturally need to define an alarm object here, for example:

[source,alan]
--------------------------------------------------------------------------------
The alarm IsA device
  Is 'on'.
  Verb examine
    Does only "The main alarm switch is a small metal lever."
  End verb.
End the.
--------------------------------------------------------------------------------



== Nested locations

Nesting locations is straightforward, as described in the ALAN Manual:

[[example.house-nested-locations]]
[source,alan]
--------------------------------------------------------------------------------
The house IsA location
End the house.

The kitchen Isa location at house
End the kitchen.

The bedroom Isa location at house
End the bedroom.

The livingroom Isa location at house
End the livingroom.
--------------------------------------------------------------------------------



This is handy when you want for example a certain OBJECT to be found in many similar LOCATIONs but don't want to implement the OBJECT in each of them separately:

[source,alan]
--------------------------------------------------------------------------------
The ceiling_lamp IsA object at house
End the.
--------------------------------------------------------------------------------

The `ceiling_lamp` would now be found in the kitchen, bedroom and living-room.
Remember, however, that if you implement a takeable OBJECT this way, the OBJECT will disappear from the other LOCATIONs when the hero takes it, and if the OBJECT will be affected in some way, for example broken, it will be broken in all of the LOCATIONs it is found in.
Also, the ceiling lamp in the above example, if implemented as a LIGHTSOURCE, would be lit/unlit in all of the three locations simultaneously.


[NOTE]
=============
The DESCRIPTION of an OBJECT implemented this way won't show automatically in the nested LOCATIONs.
You'll have to mention it manually in the description of individual locations.
=============

If you want a certain object to be present in all indoor or in all outdoor locations of your game, you can define:

[source,alan]
--------------------------------------------------------------------------------
The sun IsA object
  Is distant.
End the.
--------------------------------------------------------------------------------

or

[source,alan]
--------------------------------------------------------------------------------
The carpet IsA object
  Is scenery.
End the.
--------------------------------------------------------------------------------

(Indoor and outdoor are library-defined locations.
All ROOMs are nested in indoor and all SITEs are nested in outdoor.
Also, the objects wall, floor and ceiling are located in indoor, and the ground and the sky are in outdoor.
That's why the wall, floor and ceiling objects are found in every ROOM, and the ground and sky objects are found in every SITE.)


If you want a certain object to be present in absolutely every location of your game, you should define for example

[source,alan]
--------------------------------------------------------------------------------
The sea IsA OBJECT at my_game
End the.
--------------------------------------------------------------------------------

[NOTE]
================================================================================
For SITEs and ROOMs to work correctly when nested, the parent location should be of the same kind as the nested locations.
For example, in the example above, if you declare the kitchen, the bedroom and the living-room to be ROOMs, the house instance should also be declared a ROOM.
Sometimes this can bring problems: say you have a driveway location, with a nested location where you are inside your car.
The driveway would naturally be a SITE (outdoor location), while the inside of your car is more naturally a ROOM.
The best way to solve this is to make both of these locations just LOCATIONs and implement your own floor, walls and ceiling objects for the inside of the car, and your own ground and sky objects for the driveway.
In fact, you really don't need the walls instance for the car interior, as one would normally refer to the car doors, not to any walls:

[source,alan]
--------------------------------------------------------------------------------
The driveway IsA location
End the driveway.

The driveway_ground IsA object at driveway
  Name ground
End the.

The driveway_sky IsA object at driveway
  Name sky
End the.

The inside_car IsA location at driveway
End the inside_car.

The car_floor IsA object at inside_car
  Name floor
End the.

The car_ceiling IsA object at inside_car
  Name ceiling
End the.
--------------------------------------------------------------------------------
================================================================================

// @FIXME: Change XREf to page number:

Using nested locations, you can also make selected locations behave in a similar way.
Going back to <<example.house-nested-locations,the previous house example>>, you could define:

[source,alan]
--------------------------------------------------------------------------------
The house IsA location
  Verb jump
    Does only "It's better to jump outdoors."
  End verb.
End the.
--------------------------------------------------------------------------------

and this message would display for [.play]#&gt; _jump_# if the player tried that action in the bedroom, in the kitchen or in the living-room.

You can also check if the hero is in a certain area, or group of locations:

[source,alan]
--------------------------------------------------------------------------------
If location of hero at house
  then...
--------------------------------------------------------------------------------

or

[source,alan]
--------------------------------------------------------------------------------
Check location of hero at house
  else...
--------------------------------------------------------------------------------


Thus, we could have for example

[source,alan]
--------------------------------------------------------------------------------
Schedule explosion at hero after 5.

Event explosion
  If location of hero at house -- (house is the "area" where the hero
                               -- is located)
    then "From an open window, you hear an explosion out in the street."
  ElsIf hero at garden
    then "You hear an explosion nearby."
  end if.
end event.
--------------------------------------------------------------------------------

The locations nested in a certain parent location don't have to be _adjacent_ (i.e. connected to each other by exits).
Bearing this in mind, you can group even very different and distant locations together, as long as you want a certain object to be found, a certain verb outcome to happen, or even a certain event to take place only in those locations and not anywhere else in the game:

[source,alan]
--------------------------------------------------------------------------------
The thief_area IsA room
End the.

The round_cave IsA room at thief_area
End the.

The inn IsA room at thief_area
End the.

The train_carriage2 IsA room at thief_area
End the.

EVENT thief_appears
  IF location of hero at thief_area
    Then "A thief appears suddenly from nowhere and snatches something
          from you!"
         Locate random in hero in thief.
          -- (This line would locate a random object from the hero's
          -- inventory in the possession of the thief.)
    End if.
  Schedule thief_appears at hero after random 10 to 20.
End event.
--------------------------------------------------------------------------------


Note that the event is scheduled to trigger `at hero`.
If you defined `Schedule thief_appears at thief_area after 10 to 20.` above, the event would trigger only in the parent location thief_area which the hero never actually visits (it's just the name of the "`area`" where the actual locations where the thief appears are nested) and thus the event would be invisible to the player.
Events are not in scope in "`parent locations`" of nested locations.

// EOF //
