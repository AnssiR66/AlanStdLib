-- ALAN Standard Library v2.2.0                                    ALAN 3.0beta7
--==============================================================================

-- "The Basement" by Anssi Räisänen, 2020.

-- An example of how to implement a dark place using the `lit` attribute on
-- ordinary locations, and add an external light switch by which the hero can
-- control its illumination.

--==============================================================================

Import 'library.i'.

The my_game IsA definition_block
  Has title  "The Basement".
      author "Anssi Räisänen".
End the.
--------------------------------------------------------------------------------
-- tag::unlit-room-example[]
The stairs_top IsA room
  Name 'At the top of the stairs'
  Exit down to basement.
End the.

The light_switch IsA object at stairs_top
  Name light switch. Name light.
  Is not 'on'.
  Verb turn_on
    Does only
      If light_switch is not 'on'
        then make light_switch 'on'.
             make basement lit.
             "You switch on the basement light."
        else "The light is already switched on."
      End if.
  End verb.
-- end::unlit-room-example[]

  Verb turn_off
    Does only
      If light_switch is 'on'
        then make light_switch not'on'.
             make basement not lit.
             "You switch off the basement light."
        else "The light is already switched off."
      End if.
  End verb.
-- tag::unlit-room-example[]
End the.

The basement IsA ROOM -- and not a DARK_LOCATION
  Is not lit.
  Description "Cobwebs and old junk are the only things you see here."
  Exit up to stairs_top.
End the basement.
-- end::unlit-room-example[]

-- We'll provide the Hero with a light source, to demonstrate that it doesn't
-- have any effect on unlit locations that are not DARK_LOCATION instances.

-- tag::unlit-room-example-lightsource[]
The torch IsA lightsource in Hero.
  Is not natural.
End the.
-- end::unlit-room-example-lightsource[]
--------------------------------------------------------------------------------
Start at stairs_top.
Describe banner.
