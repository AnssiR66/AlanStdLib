////
********************************************************************************
*                                                                              *
*                     ALAN Standard Library User's Manual                      *
*                                                                              *
*                                  Chapter 6                                   *
*                                                                              *
********************************************************************************
////


[[ch6]]
= Objects


== Pre-defined object classes in the library

* `CLOTHING`
* `DEVICE`
* `DOOR`
* `LIQUID`
* `LISTED_CONTAINER`
* `SOUND`
* `SUPPORTER`
* `WEAPON`
* `WINDOW`

[NOTE]
================================================================================
The `BACKGROUND` and `SCENERY` classes introduced in v1.0 have been removed.
For backgrounds, use the `distant` or `NOT reachable` attributes.
For scenery objects, use the attribute `scenery`.
================================================================================

// @ADDED:
=== CLOTHING


CLOTHING is a piece of clothing the hero or an NPC wears.
As far as the hero is concerned, clothes are prevented from being worn in an illogical order, for example you cannot put on a shirt if you are already wearing a jacket, and so forth.
For this, clothing objects have a number of numerical attributes that need to be used.
Note that NPCs cannot wear clothing in layers.

Thanks to Alan Bampton from whose '`xwear`' extension the code for this class has been adopted.

A piece of clothing in your game code should look something similar to the following four examples.
Explanations are given after the examples.

[source,alan]
--------------------------------------------------------------------------------
THE jacket ISA CLOTHING AT lobby
  IS topcover 32.
END THE.
--------------------------------------------------------------------------------


Use `IN` to refer to containers:

[source,alan]
--------------------------------------------------------------------------------
THE jeans ISA CLOTHING IN wardrobe
  IS botcover 16.
END THE.
--------------------------------------------------------------------------------


Worn by the player character (hero):

[source,alan]
--------------------------------------------------------------------------------
THE hat ISA CLOTHING
  IS headcover 2.
END THE.

The hero ISA ACTOR
  IS wearing {hat}.
END THE hero.
--------------------------------------------------------------------------------


Worn by an NPC called Joe:

[source,alan]
--------------------------------------------------------------------------------
THE sweater ISA CLOTHING
  IS NOT takeable.
  -- if the hero is not meant to take the NPCs clothing, it is important
  -- to declare the piece of clothing to be NOT takeable.
  -- it's not necessary to state a topcover attribute here,
  -- as NPCs cannot wear clothing in layers.
END THE.

THE joe ISA ACTOR AT room1
  IS wearing {sweater}.
END THE joe.
--------------------------------------------------------------------------------



If you need the hero, or another ACTOR, to just carry a piece of clothing in their hands but not wear it, declare

[source,alan]
--------------------------------------------------------------------------------
THE hat ISA CLOTHING IN hero
  IS headcover 2.
END THE.

THE jacket ISA CLOTHING IN man
  IS topcover 32.
END THE.
--------------------------------------------------------------------------------


Note above that you must list the CLOTHING worn by an actor in a set named IS wearing.

Note that if the piece of CLOTHING worn by an NPC is not meant to be takeable by the player character, you should declare the piece of clothing to be NOT takeable.

To recap: in defining a piece of CLOTHING, you should:

1. define it ISA CLOTHING (and not ISA OBJECT)

2. give it one of five attributes headcover, topcover, botcover, footcover or handcover; sometimes two of these are needed.
Which attribute(s) to use depends on the type of CLOTHING; see the clothing table below.

3. A number 2, 4, 8, 16, 32 or 64 needs to be added after the above attribute.
You cannot decide the number yourself; look it up from the clothing table below.
If the value of an attribute for a piece of CLOTHING is 0 in the table, don't mention this attribute in connection with your CLOTHING object.

4. If the piece of CLOTHING is worn by any ACTOR -- the hero or somebody else -- be sure to include the piece of CLOTHING in the ACTOR's wearing attribute.

-- The above is enough; the rest is then handled automatically by the library.




==== The clothing table

Here is the chart showing a selection of fairly typical clothing items and the values to set to obtain appropriate behaviour.
Should you wish to create an article of CLOTHING not listed, usually a bit of lateral thought as to what it is most like and where it fits into the scheme of things will suggest a workable set of values, but be aware that you MUST use values in this chart, simply adding things with intermediate values is probably going to create nasty bugs:

[cols="<25d,5*^15m",options="header"]
|=========================================================================
| Clothing       | headcover | topcover | botcover | footcover | handcover

| hat                  |   2 |        0 |        0 |         0 |         0
| vest/bra             |   0 |        2 |        0 |         0 |         0
| undies/panties       |   0 |        0 |        2 |         0 |         0
| teddy                |   0 |        4 |        4 |         0 |         0
| blouse/shirt/T-shirt |   0 |        8 |        0 |         0 |         0
| dress/coveralls      |   0 |        8 |       32 |         0 |         0
| skirt                |   0 |        0 |       32 |         0 |         0
| trousers/shorts      |   0 |        0 |       16 |         0 |         0
| sweater/pullover     |   0 |       16 |        0 |         0 |         0
| jacket               |   0 |       32 |        0 |         0 |         0
| coat                 |   0 |       64 |       64 |         0 |         0
| socks/stockings      |   0 |        0 |        0 |         2 |         0
| tights/pantiehose    |   0 |        0 |        8 |         2 |         0
| shoes/boots          |   0 |        0 |        0 |         4 |         0
| gloves               |   0 |        0 |        0 |         0 |         2
|=========================================================================



The library, as it stands, also prevents wearing of duplicate clothes, or things that are logically mutually exclusive -- for example the player can wear a dress or a skirt, but not both.

[[sec.clothing-tips]]
==== Clothing Tips & Tricks

The hero's CLOTHING is described when the player types [.play]#&gt; _inventory_# in-game.
If you want the listing of the hero's clothing to appear also when the player types [.play]#&gt; _x me_#, add "LIST worn." to the response of the examine verb of the hero:

// @TODO: Highlight "LIST Worn":
[source,alan]
--------------------------------------------------------------------------------
THE hero ISA ACTOR
  VERB examine
    DOES ONLY "You're the prime minister of Pospia."
              LIST worn.
  END VERB.
END THE hero.
--------------------------------------------------------------------------------

Clothes can be tricky to implement when you have multiple clothing items with the same name that need to be real game objects.
To avoid disambiguation problems, authors should always try to give to each character a unique clothing item (e.g. jeans vs. sweatpants) or provide some qualifier that distinguishes them (e.g. red shirt vs green shirt); but in some cases this is not possible, for example when dealing with characters wearing identical uniforms.
As a last resort, you can always use the character's name as the disambiguating qualifier.

The following excerpts from {wedding_alan} demonstrate how to implement a game in which the hero and another NPC are both wearing tuxedos:

// @EXTERN CODE: wedding -> tuxedos

[[example.tuxedos]]
.Example: Implementing same-named clothing items (from {wedding_alan})
[source,alan]
--------------------------------------------------------------------------------
include::{utf8dir}/wedding.alan[tag=tuxedos]
--------------------------------------------------------------------------------

By adding `your`/`my` and `his`/`'sam''s'` before `tuxedo` in the `NAME` clauses, we've ensured that the player will be able to target any tuxedo in his commands:

[example,role="gametranscript"]
================================================================================
include::{utf8dir}/wedding.a3ADocLog[tag=tuxedos]
================================================================================


// @ADDED:
=== DEVICE

DEVICE is a machine or an electronic device, for example a TV.
It can be turned (=switched) on and off if it is not broken.
Default attributes: NOT on, NOT broken.
A DEVICE is by default described as being either on or off when examined.


For example:

[source,alan]
--------------------------------------------------------------------------------
include::{utf8dir}/lab1.alan[tag=thingummyjig]
--------------------------------------------------------------------------------

yields by default

[example,role="gametranscript"]
================================================================================
include::{utf8dir}/lab1.a3ADocLog[tag=thingummyjig]
================================================================================

// [example,role="gametranscript"]
// ================================================================================
// &gt; _x thingummyjig_ +
// You notice nothing unusual about the thingummyjig.
// It is currently off.
// ================================================================================


Using the `ex` attribute, you can define a bit more personal response than [.play]#You notice nothing unusual...#:

[source,alan]
--------------------------------------------------------------------------------
include::{utf8dir}/lab2.alan[tag=thingummyjig]
--------------------------------------------------------------------------------

// [source,alan]
// --------------------------------------------------------------------------------
// THE thingummyjig ISA DEVICE AT lab
//   HAS ex "It's full of knobs and buttons.".
// END THE.
// --------------------------------------------------------------------------------


yields

[example,role="gametranscript"]
================================================================================
include::{utf8dir}/lab2.a3ADocLog[tag=thingummyjig]
================================================================================

// [example,role="gametranscript"]
// ================================================================================
// &gt; _x thingummyjig_ +
// It's full of knobs and buttons.
// It is currently off.
// ================================================================================


// @FIXME: Rephrase next 2 paragraphs:
//  -- "with the mention of its being on or off ..."
//  -- "the mention of its being on or off is not..."

with the mention of its being on or off being displayed automatically.

If you don't use the `ex` attribute but define the examine response to a DEVICE using `VERB examine DOES ONLY`, the mention of its being on or off is not automatically displayed.
You have to add it manually:

[source,alan]
--------------------------------------------------------------------------------
THE thingummyjig ISA DEVICE AT lab
  VERB examine
    DOES "It's full of knobs and buttons."
      IF THIS IS NOT 'on'
        THEN "It is currently off."
        ELSE "It is currently on."
      END IF.
  END VERB.
END THE.
--------------------------------------------------------------------------------



// @ADDED:
=== DOOR


DOOR can be opened, closed, locked and unlocked.
It is by default closed (= NOT open) and NOT locked.
Attributes: openable, NOT open, NOT lockable, NOT locked, HAS otherside door.
A DOOR is described by default as being either open or closed when examined.

[source,alan]
--------------------------------------------------------------------------------
include::{utf8dir}/garden1.alan[tag=front_door]
--------------------------------------------------------------------------------


// [source,alan]
// --------------------------------------------------------------------------------
// THE front_door ISA DOOR AT garden
//   NAME front door
//   DESCRIPTION ""
// END THE front_door.
// --------------------------------------------------------------------------------


would yield by default:

[example,role="gametranscript"]
================================================================================
include::{utf8dir}/garden1.a3ADocLog[tag=front_door]
================================================================================

// [example,role="gametranscript"]
// ================================================================================
// &gt; _x front door_ +
// You notice nothing unusual about the front door.
// It is currently closed.
// ================================================================================


Adding an ex attribute for the front door, you can change the default.
Adding

[source,alan]
--------------------------------------------------------------------------------
include::{utf8dir}/garden2.alan[tag=front_door]
--------------------------------------------------------------------------------

// [source,alan]
// --------------------------------------------------------------------------------
//   HAS ex "It's a white wooden door leading into the house."
// --------------------------------------------------------------------------------


will yield


[example,role="gametranscript"]
================================================================================
include::{utf8dir}/garden2.a3ADocLog[tag=front_door]
================================================================================

// [example,role="gametranscript"]
// ================================================================================
// &gt; _x front door_ +
// It's a white wooden door leading into the house.
// It is currently closed.
// ================================================================================


If you add a response of your own to the examine verb of a DOOR, the default description of it being either open or closed won't show automatically.
You should add the description manually, like this:

[source,alan]
--------------------------------------------------------------------------------
THE front_door ISA DOOR AT garden
  NAME front door
  DESCRIPTION ""

  VERB examine
    DOES ONLY
      "It's a white wooden door leading into the house."
      IF front_door IS NOT open
        THEN "It is currently closed."
        ELSE "It is currently open."
      END IF.
  END VERB.
END THE front_door.
--------------------------------------------------------------------------------

// @NOTE: Is the transcript below *really* needed? The result is identical, so
//        we could just delete what follows and add above "to achieve the same
//        result by adding a response...". The problem with the sentence below
//        is that it gives the false impression that something different happens!

and then, the following will happen:

[example,role="gametranscript"]
================================================================================
&gt; _x door_ +
It's a white wooden door leading into the house.
It is currently closed.
================================================================================



==== Locked doors and keys

To unlock a locked DOOR, it has to have a `matching_key` object attributed to it.
Only this object can unlock the DOOR.

[source,alan]
--------------------------------------------------------------------------------
THE wooden_door ISA DOOR AT cellar
  NAME wooden door
  IS lockable. IS locked.
  HAS matching_key iron_key.
END THE wooden_door.

THE iron_key ISA OBJECT IN bedroom_drawer
END THE iron_key.
--------------------------------------------------------------------------------


By default, it will possible to unlock the DOOR both with [.play]#&gt; _unlock door_# and [.play]#&gt; _open door_# (if the player character is carrying the correct key at the time) as well as with the longer formulations [.play]#&gt; _unlock door with key_# and [.play]#&gt; _open door with key_#.

However, it is not possible to make this automatic by using compass directions only.
For example, if the DOOR was to the east of the hero, the command [.play]#&gt; _e_# cannot recognize on the library level whether the hero is carrying the key or not.
The author must implement this manually, for example:


[source,alan]
--------------------------------------------------------------------------------
THE livingroom ISA ROOM
  EXIT east TO kitchen
    CHECK kitchen_door IS NOT locked
      ELSE
      IF copper_key IN hero
        THEN "You unlock the door, open it and enter the kitchen."
          LOCATE hero AT kitchen.
          MAKE kitchen_door NOT locked.
          MAKE kitchen_door open.
        ELSE "You cannot go through the locked door."
      END IF.
  END EXIT.
END THE.
--------------------------------------------------------------------------------

Every DOOR between two rooms needs an `otherside` attribute in order for the other side of the DOOR to behave correctly when the DOOR is opened, closed, unlocked and locked.

The otherside of a DOOR need not have its other side defined any longer, as the library makes the deduction that if a DOOR has an `otherside`, this other side will have the original DOOR as its otherside in turn.
Also, the `lockable`/`locked`/`NOT locked`/`openable`/`open`/`NOT open` attributes of a DOOR instance will be automatically assumed to be the same for its otherside counterpart at the start of a game.
The same applies also to the `matching_key` attribute.
That's why it is much shorter to implement the otherside instance of a DOOR:

[source,alan]
--------------------------------------------------------------------------------
THE wooden_door1 ISA DOOR AT room1
  NAME wooden door
  IS lockable. IS locked.
  HAS matching_key iron_key.
  HAS otherside wooden_door2.
END THE locked_door.

THE wooden_door2 ISA DOOR AT room2
  NAME wooden door
END THE.
--------------------------------------------------------------------------------

Above, the `wooden_door2` is also lockable and locked at the start of the game, has `wooden_door1` as its `otherside` and can be opened with `iron_key`.
(It wouldn't mess things up even if you did declare all of these attributes under `wooden_door2`, to be sure, but it is not necessary.)

See also <<ch15>>, <<ch15.example5,example 5>>.



=== LIQUID

LIQUID can be taken only if it is in a container.
You can fill something with it, and you can pour it somewhere.
A LIQUID is by default `NOT drinkable`.


If you have some LIQUID in a container in your game, you should declare it this way:

[source,alan]
--------------------------------------------------------------------------------
THE juice ISA LIQUID
  IN bottle
END THE juice.
--------------------------------------------------------------------------------

Then, taking and pouring LIQUIDs work smoothly.

The verb `pour`, as defined in this library, also works for the container of a LIQUID; i.e. if there is some juice in a bottle, [.play]#&gt; _pour bottle_# and [.play]#&gt; _pour juice_# will work equally well.
Note, however, that the verb `empty` is not a synonym for `pour`; `empty` only works for container objects.
Consequently, [.play]#&gt; _empty bottle_# will work but [.play]#&gt; _empty juice_# won't.


=== LIGHTSOURCE

LIGHTSOURCE is `natural` or `NOT natural` (a natural LIGHTSOURCE is for example a match or a torch).
It can be turned on and off, lighted and extinguished (= put out) if it is not broken.
A natural LIGHTSOURCE cannot be turned on or off, it can only be lighted and extinguished (= put out).
When examined, a LIGHTSOURCE is by default supplied with a description of whether it is providing light or not.
The default attributes for a LIGHTSOURCE object are: `natural`, `NOT lit`.

// 'torch' -> LIGHTSOURCE (LIT) AT cave:

[source,alan]
--------------------------------------------------------------------------------
include::{utf8dir}/cave1.alan[tag=lightsource_torch]
--------------------------------------------------------------------------------

// 'lamp' -> LIGHTSOURCE (NOT natural) AT lab:

[source,alan]
--------------------------------------------------------------------------------
include::{utf8dir}/lab1.alan[tag=lightsource_lamp]
--------------------------------------------------------------------------------


Examining for example these instances in-game would yield

[example,role="gametranscript"]
================================================================================
include::{utf8dir}/cave1.a3ADocLog[tag=lightsource_torch]
================================================================================

and

[example,role="gametranscript"]
================================================================================
include::{utf8dir}/lab1.a3ADocLog[tag=lightsource_lamp]
================================================================================


(The opposites of the above messages would be [.play]#It is currently not lit.# and [.play]#It is currently on.#, respectively.)

If you add a specific examine response for either, you can use the `ex` attribute for the lightsource object to describe it, and after your description there will be an automatic description of its being lit or not lit:

// 'lamp' -> LIGHTSOURCE (NOT natural) AT lab + HAS ex:

[source,alan]
--------------------------------------------------------------------------------
include::{utf8dir}/lab2.alan[tag=lightsource_lamp]
--------------------------------------------------------------------------------

which yields

[example,role="gametranscript"]
================================================================================
include::{utf8dir}/lab2.a3ADocLog[tag=lightsource_lamp]
================================================================================

Using `VERB examine DOES ONLYâ€¦` you'll have to add manually the mention about the instance being on or off, for example:

// @EXTERNALIZE CODE: bedroom -> LIGHTSOURCE -> VERB examine ???
[source,alan]
--------------------------------------------------------------------------------
THE lamp ISA LIGHTSOURCE AT bedroom
  IS NOT natural.
  VERB examine
    DOES ONLY "It is an elegant table lamp with a blue
               lampshade."
      IF THIS IS NOT lit
        THEN "It is currently off."
        ELSE "It is currently on."
      END IF.
  END VERB.
END THE lamp.
--------------------------------------------------------------------------------

or

// @EXTERNALIZE CODE: cave -> LIGHTSOURCE -> VERB examine ???
[source,alan]
--------------------------------------------------------------------------------
THE torch ISA LIGHTSOURCE AT cave
  IS lit.
  VERB examine
    DOES ONLY "It is a crude wooden torch."
      IF THIS IS NOT lit
        THEN "It is currently not lit."
        ELSE "It is currently lit."
      END IF.
  END VERB.
END THE torch.
--------------------------------------------------------------------------------


=== LISTED_CONTAINER

`LISTED_CONTAINER` is an object which has the `container` property.
The contents of a `LISTED_CONTAINER` will be listed both after [.play]#&gt; _look_# (= in the room description), [.play]#&gt; _look in_# and [.play]#&gt; _examine_#, if it is open.
(The contents of a normal container object, as working by default in Alan 3, are not automatically listed after [.play]#&gt; _examine_# but only after [.play]#&gt; _look_# (=room description) and [.play]#&gt; _look in_#).

To implement a `LISTED_CONTAINER` do for example like this:

[source,alan]
--------------------------------------------------------------------------------
THE box ISA LISTED_CONTAINER AT room1
END THE box.
--------------------------------------------------------------------------------

The contents of a `LISTED_CONTAINER` are also listed when it is opened.
This doesn't happen with normal containers (= OBJECTs that you give the `container` property).
For the command [.play]#&gt; _inventory_# to list the contents of a `LISTED_CONTAINER` object the hero is carrying, redefine the verb `inventory` under the `my_game` instance in your source file for example this way:

[source,alan]
--------------------------------------------------------------------------------
VERB i
  DOES
    IF bag IN hero
      THEN LIST bag.
    END IF.

    IF box IN hero
      THEN LIST box.
    END IF.
END VERB.
--------------------------------------------------------------------------------



If you don't do this, the bag and the box will be listed after the command [.play]#&gt; _inventory_# in the following way:

[example,role="gametranscript"]
================================================================================
You are carrying a bag and a box.
================================================================================


only.
But with the above additions, the outcome is for example

[example,role="gametranscript"]
================================================================================
You are carrying a bag and a box.
The bag contains a loaf of bread.
The box is empty.
================================================================================

To declare a LISTED_CONTAINER the contents of which should not be listed after [.play]#&gt; _look_# or [.play]#&gt; _examine_#, declare it an `opaque container` in the following way:

[source,alan]
--------------------------------------------------------------------------------
THE box ISA LISTED_CONTAINER
  OPAQUE CONTAINER
END THE.
--------------------------------------------------------------------------------

Things in an opaque container cannot be seen or manipulated.
To change this, declare for example

[source,alan]
--------------------------------------------------------------------------------
MAKE box NOT OPAQUE.
--------------------------------------------------------------------------------

(This is handled automatically by the library when a container is opened or closed.)


=== Putting things in containers

It is only possible to put something into a container if this something is included in the object.

[source,alan]
--------------------------------------------------------------------------------
THE drawer ISA LISTED_CONTAINER IN nightstand
  HAS allowed {diary, keys}.
  ...
END THE drawer.
--------------------------------------------------------------------------------

In the example above, it wouldn't be possible to put anything else in the drawer, for example a chair or a coffee cup.
The response would be for example [.play]#The coffee cup doesn't belong in the drawer.#, etc.

This applies not only to the verb `put_in` but also to `empty_in`, `pour_in` and `throw_in`.

Everything programmed to be in a container by the author at the start of the game will be automatically included in the `allowed` set of the container.
Thus, for example if the author implements an apple in a bowl and the `hero` character takes it, it will be possible for the hero to put the apple back into the bowl, without the author having to implement any separate `allowed` attributes for this to happen.
But note if you have for example a ticket dispenser in your game and the `hero` takes a ticket from it, it would be possible to put the ticket back into the dispenser, the way things work by default.
This is not what is wanted in this case.
That's why in that case you should do either:

// PAGE 49 //



[source,alan]
--------------------------------------------------------------------------------
THE ticket_dispenser ISA LISTED_CONTAINER AT lobby
  ...
  VERB put_in
    WHEN cont
      DOES "That's not possible."
  END VERB.
END THE.
--------------------------------------------------------------------------------

or, alternatively:

[source,alan]
--------------------------------------------------------------------------------
THE ticket ISA OBJECT IN ticket_dispenser
  INITIALIZE
    EXCLUDE THIS FROM allowed OF ticket_dispenser.
END THE ticket.
--------------------------------------------------------------------------------

Alternatively, you could just have the ticket available in the location in general, not having to locate it in the dispenser at all.
This would make the above coding unnecessary but would make the `take` verb a bit lengthier:

[source,alan]
--------------------------------------------------------------------------------
THE ticket ISA OBJECT AT lobby
  IS in_the_dispenser.
  VERB take
    CHECK ticket IS in_the_dispenser
      ELSE "You already took a ticket."
    DOES ONLY "You take a ticket from the dispenser."
      MAKE ticket NOT in_the_dispenser.
      LOCATE ticket IN hero.
  END VERB.
END THE.

THE ticket_dispenser ISA LISTED_CONTAINER AT lobby
END THE.
--------------------------------------------------------------------------------

=== SOUND

`SOUND` can be listened to but not examined, searched, smelled or manipulated.
It cannot initially be turned on or off, this has to be implemented manually by giving the sound the `switchable` attribute.

[source,alan]
--------------------------------------------------------------------------------
THE siren ISA SOUND AT bedroom
  DESCRIPTION "The sound of a siren can be heard outside in the street."
END THE.

THE alarm_clock_sound ISA SOUND AT bedroom
  NAME alarm clock sound NAME alarm clock
  IS switchable.
  IS 'on'.
END THE.
--------------------------------------------------------------------------------


=== SUPPORTER

`SUPPORTER`: You can put things on a SUPPORTER and you can stand, sit down or lie on it.

A SUPPORTER is declared to be a container, so that you can take things from it, as well.
When there's something on a SUPPORTER, a listing of it will appear in the room description and after [.play]#&gt; _examine_#, by default:

[example,role="gametranscript"]
================================================================================
&gt; _look_

Bedroom

There is a nightstand here.
On the nightstand you see a diary.
================================================================================


To implement OBJECTs on a SUPPORTER, define the SUPPORTER first; for example

[source,alan]
--------------------------------------------------------------------------------
THE tray ISA SUPPORTER
END THE.
--------------------------------------------------------------------------------

Then, implement the OBJECTs on the supporter like this:

[source,alan]
--------------------------------------------------------------------------------
THE apple ISA OBJECT
  IN tray
END THE.
--------------------------------------------------------------------------------

WARNING: @FIXME: this sentence in incomplete -- "even if ... What?"

Note the IN above, even if the `apple` will be described as being _on_ the tray [???].
Similarly, to implement a book on a table:

[source,alan]
--------------------------------------------------------------------------------
THE table ISA SUPPORTER AT livingroom
END THE table.

THE book ISA OBJECT
  IN table
END THE.
--------------------------------------------------------------------------------

// PAGE 51 //



Note that the [.play]#&gt; _examine_# command will list what is on the surface of a SUPPORTER, not what, if anything, is inside the SUPPORTER.
For example, if you have a SUPPORTER called `table` in your game with two drawers in it, DON'T do this:

[source,alan]
--------------------------------------------------------------------------------
THE drawer1 ISA OBJECT
  NAME bottom drawer
  CONTAINER
    IN table.
END THE.
--------------------------------------------------------------------------------

or this:

[source,alan]
--------------------------------------------------------------------------------
THE drawer2 ISA LISTED_CONTAINER
  NAME top drawer
  IN table.
END THE.
--------------------------------------------------------------------------------

This would result in something like "`There's a table here. On the table you see a book, a bottom drawer and a top drawer.`"

Instead, do the following:

[source,alan]
--------------------------------------------------------------------------------
THE table ISA SUPPORTER
  AT bedroom
  HAS components {drawer1, drawer2}. -- 'components' is not a pre-defined
                                     -- attribute in the library, it is just used
                                     -- in this example. You could name this
                                     -- attribute in any other way, too.
  VERB examine
    DOES
      FOR EACH c IN components OF THIS DO
        SAY "The table has" SAY AN c. "."
        IF c IS open
          THEN LIST c.
          ELSE SAY THE c. "is closed."
        END IF.
      END FOR.
  END VERB.
END THE.

THE drawer1 ISA LISTED_CONTAINER
  OPAQUE CONTAINER
  DESCRIPTION ""
  NAME bottom drawer
  AT bedroom
  IS NOT open.
END THE.

THE drawer2 ISA LISTED_CONTAINER
  OPAQUE CONTAINER
  DESCRIPTION ""
  NAME top drawer
  AT bedroom
  IS open.
END THE.

THE book ISA OBJECT IN table
END THE book.

THE diary ISA OBJECT IN drawer2
END THE diary.
--------------------------------------------------------------------------------

In other words, declare the drawers components of the table, in the manner described above.
The result will then be for example something like this:

[example,role="gametranscript"]
================================================================================
&gt; _l_

There is a table here. On the table you see a book.

&gt; _x table_

You notice nothing unusual about the table. On the table you see a book.
The table has a bottom drawer. The bottom drawer is closed.
The table has a top drawer. The top drawer contains a diary.
================================================================================

If you want to get rid of the default "`You see nothing unusual...`" message above, edit the response to the `examine` verb for example this way:

// PAGE 53 //



[source,alan]
--------------------------------------------------------------------------------
THE table ISA SUPPORTER
  AT bedroom
  HAS components {drawer1, drawer2}.
  VERB examine
    DOES ONLY
      "It's an antique oak table."
      LIST table.
      FOR EACH c IN components OF THIS DO
        SAY "The table has" SAY AN c. "."
        IF c IS open
          THEN LIST c.
          ELSE SAY THE c. "is closed."
        END IF.
      END FOR.
  END VERB.
END THE.
--------------------------------------------------------------------------------

The code `LIST [supporter].` will list what the supporter has on its surface.

Standing, sitting or lying down on a SUPPORTER is not allowed by default, however, but must be manually implemented by the author:

[source,alan]
--------------------------------------------------------------------------------
THE bed ISA SUPPORTER AT bedroom
  VERB lie_on
    DOES ONLY
      "You lie down on the bed."
      MAKE hero lying_down.
  END VERB.
END THE.
--------------------------------------------------------------------------------

Remember that it is not possible to locate an ACTOR inside an OBJECT, for example in a bed container.
Using the `sitting` or `lying_down` attributes should be enough to account for these situations and to create the impression that the `hero` is located on a SUPPORTER object.
When the hero is made `sitting` or `lying_down`, certain actions are disabled by the library (for example `attack`, `jump` etc.).
It is the author's responsibility to make certain objects in the location `NOT reachable` as needed, while the hero is lying down or sitting, and also to prohibit movement or at least implement a clarifying message of the hero standing up, before going in any direction.

// PAGE 54 //




=== WEAPON

WEAPON is `fireable` (for example a cannon) or `NOT fireable` (for example a baseball bat), the latter being the default.
The verbs `attack_with` and `kill_with` won't have successful outcomes if the second parameter in them is not a WEAPON.
(Even when the second parameter is a WEAPON, the outcome of the action is not successful by default.

You must implement a successful outcome manually at the instance level.)

[source,alan]
--------------------------------------------------------------------------------
THE pistol ISA weapon IN room1
  IS fireable.
END THE.
--------------------------------------------------------------------------------


=== WINDOW

WINDOW can be opened, closed, looked through and out of.
It will be described as being either open or closed when examined, by default.
It is by default `NOT open`.

[source,alan]
--------------------------------------------------------------------------------
THE bedroom_window ISA WINDOW AT bedroom
  NAME bedroom window
  IS open.
END THE.
--------------------------------------------------------------------------------

yields

[example,role="gametranscript"]
================================================================================
&gt;  _x bedroom window_

You notice nothing unusual about the bedroom window. It is currently closed.
================================================================================

If you add a specific `examine` response for a WINDOW instance, you have to add manually the mention about the instance being open or closed, for example:

[source,alan]
--------------------------------------------------------------------------------
THE bedroom_window ISA WINDOW AT bedroom
  NAME bedroom window
  IS open.
  VERB examine
    DOES ONLY "It's a big window facing east to the garden."
      IF THIS IS NOT open
        THEN "It is currently closed."
        ELSE "It is currently open."
      END IF.
  END VERB.
END THE bedroom_window.
--------------------------------------------------------------------------------

// PAGE 55 //


// EOF //

