-- ALAN Standard Library v2.2.0                                    ALAN 3.0beta8
--==============================================================================

-- "The Living Room" by Anssi Räisänen, 2020.

-- An example of how to override the default descriptions of a ROOM's floor,
-- walls and ceiling objects, and further change them mid-game as a result of
-- some player action..

--==============================================================================
Import 'library.i'.

The my_game IsA definition_block
  Has title  "The Living Room".
      author "Anssi Räisänen".
End the.
--------------------------------------------------------------------------------
The livingroom IsA room
  Has floor_desc   "There is an exquisite white carpet on the floor.".
  Has walls_desc   "The wallpaper has a nice flower pattern.".
  Has ceiling_desc "A huge chandelier is hanging from the ceiling.".
End the livingroom.

The carpet IsA object at livingroom
  Verb move
    Check trapdoor not at livingroom
      else "There is no need to move the carpet any longer."
    Does only
      "You move the carpet, revealing a trapdoor!"
      Locate trapdoor at livingroom.
      Set floor_desc of livingroom to "A trapdoor has been revealed when you
                                       moved the carpet. It leads down to
                                       darkness.".
  End verb.
End the carpet.

The trapdoor IsA door
End the trapdoor.
--------------------------------------------------------------------------------
Start at livingroom.
Describe banner.
