-- ALAN Standard Library v2.2.0-RC                                 ALAN 3.0beta7
--==============================================================================

-- "The Sewers" by Tristano Ajmone, 2020.

-- A short DARK_LOCATION example.

--==============================================================================
Import 'library.i'.

The my_game IsA definition_block
  Has title    "The Sewers".
      subtitle "A short DARK_LOCATION example.".
      author   "Tristano Ajmone".
End the.
--------------------------------------------------------------------------------
The alley IsA site.
  Name 'Dead End Alley'.
  Description "An open manhole leads down into the city's bowels."
  Has floor_desc "There's an open manhole in the pavement.".
  Exit down to sewers
    Does
      "You gather your courage and slip down the manhole, into the unknown..."
  End exit.
End the.

The manhole IsA object at alley.
  Name manhole. Name hole.
  Is not takeable.
  Has ex
    "Someone has removed the cover from the manhole.
     Why would anyone to that?
     $nYou peep through the oepn hole but can't see much, it's too dark.
     $nNow you're really curious. What's down there?".
End the manhole.

The sewers IsA dark_location.
  Name 'In the Sewers Tunnels'.
  Description
    "The air in this gloomy tunnel is filled with foul smelling fumes.
     Your eyes are itching and your lungs burning."
  Exit up to alley
    Check "The manhole it too high up, you'll need a ladder."
  End exit.
End the.

The flashlight IsA lightsource in Hero.
  Name flashlight. Name torch.
  Is not natural.
End the.
--------------------------------------------------------------------------------
Start at alley.
Describe banner.
