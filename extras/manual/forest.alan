-- ALAN Standard Library v2.2.0                                    ALAN 3.0beta7
--==============================================================================

-- "The Forest" by Tristano Ajmone, 2020.

-- A demonstration of how to use the `forced_lit` attribute of `DARK_LOCATION`
-- instances to simulate the day and night cycle.

--==============================================================================
Import 'library.i'.

The my_game IsA definition_block
  Has
    title  "The Forest".
    author "Tristano Ajmone".
End the.
--------------------------------------------------------------------------------
The forest IsA DARK_SITE.
  Name 'The Forest of Doom'.
  Is lit. forced_lit. daytime.
  Has sky_desc "Sunny and bright".
  Description
    If this is daytime then
      "The thick forest canopy absorbs most of the daylight.
       You're surrounded by a barrier of vegetation."
    else
      "At night the forest looks even more menacing under the trembling
       light of your torch -- or is it your hand that's shaking?"
    End if.

  Exit north, south, east, west, northeast, southeast, northwest, southwest
    to nowhere
      Check "The vegetation bars your passage.
             You'll never escape the forest"
  End exit.

  Verb sleep -- Overriding library default verb...
    Does only
      "You take a nap. When you wake up its"
      If this is daytime
        then "nighttime.$p"
          Set sky_desc of this to "Pitch black, no stars, no moon.".
          -- ----------------------------------------------------
          -- No need to worry about the `lit` attribute at night!
          -- The library will handle it automatically for us.
          -- ----------------------------------------------------
          Make this not forced_lit.
          Make this not daytime.
          -- Only perform a LOOK if there are lit light sources:
          If count IsA lightsource, is lit, at hero > 0
            then look.
          End if.
        else "daytime."
          Set sky_desc of this to "Sunny and bright.".
          Make this lit. Make this forced_lit.
          Make this daytime. Look.
      End if.
  End verb sleep.
End the forest.

-- =============================================================================

-- Light Sources

-- =============================================================================
-- Needless to say, we need to provide our Hero with some means to light up the
-- place during the night...

The torch IsA lightsource in Hero.
  Is not natural.
End the.
--------------------------------------------------------------------------------
Start at forest.
Describe banner.
"$pThe 'forced_lit' attribute allows to prevent the library from making dark a
 DARK_LOCATION when there are no lightsources left. It's used to enforce light
 during the day, and then unset during the night to restore the default behavior
 of DARK_LOCATION instances.

 $pThe forest is implemented as a DARK_SITE which toggles from day to night, and
 vice-versa, each time the Hero sleeps.

 $pThe player carries an electric torch (NOT natural LIGHTSOURCE) by which he
 can illuminate the forest at nighttime."
