--------------------------------------------------------------------------------
-- A complete example game with locked doors and keys.

-- This code reintroduces the situation used in example 1, with a locked door
-- and two keys added.
--------------------------------------------------------------------------------

Import 'library.i'.

The my_game IsA definition_block
End the.

The room1 IsA location
  Description "North to room2."
  Exit north to room2
    Check locked_door_1 is open
      else "The door to the north is on the way."
  End exit.
End the.

The locked_door_1 IsA door at room1
  Description ""
  Name door
  Has otherside locked_door_2.
  Is lockable. Is locked.
  Has matching_key silver_key.
End the.

The silver_key IsA object at room1
  Name silver key
End the.

The brass_key IsA object at room1
  Name brass key
End the.

The room2 IsA location
  Description "South to room1."
  Exit south to room1
    Check locked_door_2 is open
      else "The door to the south is on the way."
  End exit.
End the.

The locked_door_2 IsA door at room2
  Description ""
  Name door
End the.

The apple IsA object at room2
  Is edible.

  Verb eat
    Does "Congratulations!" quit.
  End verb.
End the.

Start at room1.
Describe banner.
