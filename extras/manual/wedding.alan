-- ALAN Standard Library v2.2.0                                    ALAN 3.0beta7
--==============================================================================

-- "The Wedding" by Anssi Räisänen and Tristano Ajmone, 2020.

-- An example of how to implement a game in which the hero and another NPC are
-- both wearing tuxedos.

--==============================================================================
Import 'library.i'.

The my_game IsA definition_block
  Has title  "The Wedding".
      author "Anssi Räisänen and Tristano Ajmone".
End the.
--------------------------------------------------------------------------------
The hero IsA actor
End the.

The your_tuxedo IsA clothing in hero
  Name your tuxedo Name my tuxedo
  Indefinite article ""
  Definite article ""
  Has ex "Sam wanted the two of you to wear identical tuxedos
          for the occasion, so you went and bought them together.".
  Is topcover 32.
  Is botcover 16.
  Is worn.
End the.

The restaurant IsA room
  Description
    "All the wedding guests follow keenly as the newly-wedded
     couple is slicing the cake together. Many are taking
     photographs."
End the.

The people IsA person at restaurant
  Name guests Name wedding folk Name people
  Mentioned "the wedding folk"
  Description ""

  Verb examine
    Does only "Everybody's smiling and laughing."
  End verb.
End the.

The couple IsA person at restaurant
  Name couple Name bride Name groom
  Description ""

  Verb examine
    Does "They look very happy."
  End verb.
End the.

The cake IsA object at restaurant
  Is edible.
  Description ""

  Verb examine
    Does only "A big, delicious-looking cream cake."
  End verb.

  Verb take
    Does only "Hands off! Wait until the couple has got their slice."
  End verb.

  Verb eat
    Does only "Hands off! Wait until the couple has got their slice."
  End verb.
End the.


The sam IsA male at restaurant
  Is named.
  Has ex "Sam and you have been best friends since childhood.
          Today he's getting married, and he asked you to be his witness.".
End the sam.

The sams_tuxedo IsA clothing in sam
  Name his tuxedo Name 'sam''s' tuxedo
  Indefinite article ""
  Definite article ""
  Has ex "Although you're wearing identical tuxedos, you can't
          avoid the feeling that Sam's nicer than yours.
          Maybe it's just the ""bridegroom effect"".".
  Is worn.

  Verb ask_for
    Does only
      "Puzzled by your request, Sam scoffs at you,
       $n""What's wrong with yours?
           We bought them together, same model and all."""
  End verb ask_for.
End the.
--------------------------------------------------------------------------------
Start at restaurant.
Describe banner.
