:: "RUNTESTS.bat" v3.0.0 (2018/11/01) MIT License
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
::                                                                            ::
::                      ALAN STANDARD LIBRARY TEST SUITE                      ::
::                                                                            ::
::                             by Tristano Ajmone                             ::
::                                                                            ::
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: For every "*.alan" file found in this folder, this script will:
::   1. Compile "<filename>.alan" to "<filename>.a3c".
::   2. Play the compiled adventure using one or more commands files associated
::      to it via the "<filename>*.a3sol" naming pattern (eg "<filename>.a3sol",
::      "<filename>_two.a3sol", "<filename>_vanilla.a3sol", etc.)
::   3. Save the play session transcript to "<commandscript>.a3log" (ie, using
::      the same name of the commands script, but with ".a3log" extension).
::   4. Print a statistics and errors report on the terminal screen.
::------------------------------------------------------------------------------
@ECHO OFF & CLS
:: =================
:: Enivronment Setup
:: =================
SETLOCAL EnableDelayedExpansion

:: Set code page to UTF-8 for handling special chars in commands scripts:
CHCP 65001 > nul 2>&1

CALL :InitANSIColors   &:: Defines some ANSI colors variables
:: =================================
:: Print Title Banner and Intro Text
:: =================================
CALL :PrintHeader      &:: Prints the title banner

ECHO This script will compile all the Alan adventure in this folder, run them against
ECHO a commands script and save the game session transcripts to file.
ECHO.
:: =======================
:: Define Script Variables
:: =======================
SET "_COMPILE_OPTS=-import ..\ -debug"
SET /A _ERR=0          &:: Errors counter for Exit Code
SET /A _CNT=0          &:: Processing operations counter
SET /A _ADVSOURCES=0   &:: Total adventures source files
SET /A _ADVCOMPILE=0   &:: Compiled adventures counter
SET /A _ADVTESTED=0    &:: Tested adventures counter
SET /A _SCRIPTSCNT=0   &:: Executed commands scripts counter
:: =========================================
:: Delete Old Files Generated by This Script
:: =========================================
DEL *.a3c    > nul 2>&1
DEL *.a3log  > nul 2>&1
DEL *.ifid   > nul 2>&1
DEL *.log    > nul 2>&1
:: ====================================
:: Calculate Total Number of Adventures
:: ====================================
ECHO The following adventures will be processed:%GRAY%
FOR %%i IN (*.alan) DO (
    SET /A _ADVSOURCES=!_ADVSOURCES! +1
    ECHO   !_ADVSOURCES!. %YELLOW%%%i%GRAY%
)
:: ================================
:: Compile Adventures and Run Tests
:: ================================
FOR %%i IN (*.alan) DO (
    SET /A _CNT=!_CNT! +1
    ECHO %BLUE%
    ECHO ================================================================================
    ECHO !_CNT!/%_ADVSOURCES% -- ^"%YELLOW%%%i%BLUE%^"
    ECHO ================================================================================
    :: Only run tests if compilation succeeded:
    CALL :CompileAdv "%%i"  &&  CALL :RunTestScript "%%i"
)
:: ==================
:: Print Final Report
:: ==================
ECHO %BLUE%
ECHO ================================================================================
ECHO SUMMARY REPORT
ECHO ================================================================================
ECHO %WHITE%Tests execution completed.
ECHO.
SET TEMP_COL=%GREEN%
:: -----------------------------------------------
:: Report total number of source adventures found:
:: -----------------------------------------------
ECHO ADVENTURES SOURCES:  %_ADVSOURCES%
:: --------------------------------------------------------
:: Report total number of adventures successfully compiled:
:: --------------------------------------------------------
IF %_ADVCOMPILE% NEQ %_ADVSOURCES% (
    SET TEMP_COL=%RED%
)
ECHO %TEMP_COL%ADVENTURES COMPILED: %_ADVCOMPILE%/%_ADVSOURCES%
:: -----------------------------------------
:: Report total number of tested adventures:
:: -----------------------------------------
SET TEMP_COL=%GREEN%
IF %_ADVTESTED% NEQ %_ADVSOURCES% (
    SET TEMP_COL=%RED%
)
ECHO %TEMP_COL%ADVENTURES TESTED:   %_ADVTESTED%/%_ADVSOURCES%
:: -------------------------------------------------
:: Report total number of commands scripts executed:
:: -------------------------------------------------
ECHO %WHITE%TESTS EXECUTED:      %_SCRIPTSCNT%
:: ------------------------------------------
:: Report total number of errors encountered:
:: ------------------------------------------
IF %_ERR% EQU 0 (
    ECHO %GREEN%ERRORS: NONE
    ) ELSE (
    ECHO %RED%ERRORS: %_ERR%
)

:: =============================
:: Wrap-Up and Exit Batch Script
:: =============================
:EXIT_SCRIPT

:: Reset variables (required due to CMD /K further on):
SET _COMPILE_OPTS=
SET _ERR=
SET _CNT=
SET _ADVSOURCES=
SET _ADVCOMPILE=
SET _ADVTESTED=
SET _SCRIPTSCNT=

:: Reset ANSI terminal colors:
ECHO %RESET_COLORS%

:: Ensure that CMD remains open if the script was launched via File Explorer:
ECHO "%cmdcmdline%" | FINDSTR /IC:"%windir%" >nul && (
    CMD /K
)
EXIT /B %_ERR%

:: *****************************************************************************
:: *                                                                           *
:: *                            SCRIPT FUNCTIONS                               *
:: *                                                                           *
:: *****************************************************************************

:: =============================================================================
::                           Compile Source Adventure                           
:: =============================================================================
:CompileAdv

CALL alan.exe %_COMPILE_OPTS% %1  > nul 2>&1 ^
    && (
        SET /A _ADVCOMPILE=%_ADVCOMPILE% +1
        ECHO %BG_GREEN%
        ECHO -----------------------
        ECHO  COMPILATION SUCCEEDED 
        ECHO -----------------------
    ) || (
        SET /A _ERR=%_ERR% +1
        ECHO %BG_RED%
        ECHO ~~~~~~~~~~~~~~~~~~~~~~~
        ECHO  COMPILATION FAILED^^!^^!^^! 
        ECHO ~~~~~~~~~~~~~~~~~~~~~~~
        ECHO %RESET_COLORS%%BLUE%
        ECHO Compiler report:%RED%
        :: Compile again in order to show compiler errors report
        CALL alan.exe %_COMPILE_OPTS% %1
    )
EXIT /B
:: =============================================================================
::                             Run Commands Script                              
:: =============================================================================
:: The adventure "<filename>.alan" of the parameter will be run against every
:: commands script that starts with "<filename>" (ie: "<filename>*.a3sol").
:: This system allows to test the same adventure with multiple test scripts.
:: At least one test script must be present, otherwise an error will be reported.
:: The minimum expectation is to find at least "<filename>.a3sol".
:: -----------------------------------------------------------------------------
:RunTestScript

SET _ADVFILE=%~n1.a3c  &:: The same adventure is used with all test scripts!
SET _SCRIPT=%~n1.a3sol &:: To be used in case of error (no script found).
:: ===============================================
:: Execute All the Tests Scripts of This Adventure
:: ===============================================
:: '%_FOUND%' tracks if at least one script was found:
SET _FOUND=

FOR /R %%i IN (%~n1*.a3sol) DO (
    SET "_FOUND=1"
    REM ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    REM NOTE: The code doesn't actually check that ARun returned without error,
    REM       or that the log was actually created. Should improve this!
    REM ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    SET /A _SCRIPTSCNT=!_SCRIPTSCNT! +1
    CALL arun.exe -r %_ADVFILE% < %%i > %%~ni.a3log
    ECHO %BG_GREEN%
    ECHO -----------------------------
    ECHO  TRANSCRIPT SAVED TO DISK^^!^^!^^! 
    ECHO -----------------------------
    ECHO %RESET_COLORS%%GREEN%
    ECHO Test transcript saved to "%YELLOW%%%~ni.a3log%GREEN%".
)
IF NOT DEFINED _FOUND (
    SET /A _ERR=%_ERR% +1
    ECHO %BG_RED%
    ECHO ~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    ECHO  MISSING COMMANDS SCRIPT^^!^^!^^! 
    ECHO ~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    ECHO %RESET_COLORS%%RED%
    ECHO Matching script "%YELLOW%%_SCRIPT%%RED%" not found.
) ELSE SET /A _ADVTESTED=!_ADVTESTED! +1
:: ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
:: NOTE: The above ELSE clause increases by one the counter of tested adventures,
::       while the FOR loop that precedes it increments the counter of total
::       tests executed.
:: ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
EXIT /B

:: =============================================================================
::                  Initialize ANSI Terminal Colors Variables                   
:: =============================================================================
:InitANSIColors

SET RESET_COLORS=[0m
:: ========================
:: Foreground Colors
:: ========================
SET BLUE=[36m
SET GRAY=[37m
SET GREEN=[92m
SET RED=[91m
SET WHITE=[97m
SET YELLOW=[93m
:: ========================
:: Colored Backgrounds
:: ========================
SET BG_GREEN=[97;102m
SET BG_RED=[97;101m
EXIT /B
:: =============================================================================
::                        Destroy ANSI Colors Variables                         
:: =============================================================================
:: Clean up env-vars before leaving script. This precaution is required because
:: of the code that ensures that CMD remains open if the script was launched via
:: File Explorer -- in that case, variables set by this script will persist.
:DestroyANSIColors

SET RESET_COLORS=
SET BLUE=
SET GRAY=
SET GREEN=
SET RED=
SET WHITE=
SET YELLOW=
SET BG_GREEN=
SET BG_RED=
EXIT /B
:: =============================================================================
::                             Print Header Banner                              
:: =============================================================================
:PrintHeader

ECHO %GREEN%
ECHO ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
ECHO ::                                                                            ::
ECHO ::%YELLOW%               ^
       ALAN STANDARD LIBRARY TEST SUITE                      %GREEN%::
ECHO ::                                                                            ::
ECHO ::%YELLOW%                      ^
       by Tristano Ajmone                             %GREEN%::
ECHO ::                                                                            ::
ECHO ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
ECHO %WHITE%
EXIT /B

::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
::                                The MIT License                             ::
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: MIT License
:: 
:: Copyright (c) 2018 Tristano Ajmone, https://github.com/tajmone
:: 
:: Permission is hereby granted, free of charge, to any person obtaining a copy
:: of this software and associated documentation files (the "Software"), to deal
:: in the Software without restriction, including without limitation the rights
:: to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
:: copies of the Software, and to permit persons to whom the Software is
:: furnished to do so, subject to the following conditions:
:: 
:: The above copyright notice and this permission notice shall be included in
:: all copies or substantial portions of the Software.
:: 
:: THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
:: IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
:: FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
:: AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
:: LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
:: OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
:: SOFTWARE.
::::::::::::::::::::::::::::::::::::{ EOF }:::::::::::::::::::::::::::::::::::::
