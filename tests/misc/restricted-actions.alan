--==============================================================================
-- "Restricted Actions Tests" by Tristano Ajmone, 2020.
--==============================================================================
IMPORT 'library.i'. -- ALAN Standard Library v2.2.0


THE my_game IsA DEFINITION_BLOCK

  HAS title    "Restricted Actions Tests".
  HAS subtitle "An adventure to test StdLib 2.2.0 restricted actions".
  HAS author   "Tristano Ajmone".
  HAS year     2020.
  HAS version  "2".

END THE.

--=============================================================================
--\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
--------------------------------------------------------------------------------
-- § 1 - Restriction Level Rooms
--------------------------------------------------------------------------------
--//////////////////////////////////////////////////////////////////////////////
--=============================================================================
-- We'll create a room for each restriction level of the Library, so we can test
-- restricted actions in each room accordingly. We also add a "level" debug verb
-- to print out the current level of restriction.

EVERY level_room IsA room.
  HAS Level 0.
  ENTERED
    SET my_game:restricted_level TO Level OF THIS.

END EVERY level_room.

--------------------------------------------------------------------------------
-- Verb LEVEL
--------------------------------------------------------------------------------
-- Let's add a verb to print the restriction level of the current location.
-- Of course, this verb isn't blocked by Restrictions Levels!

SYNTAX level = level.

VERB level
  DOES
    "Current restriction level:"
    SAY my_game:restricted_level. "."
END VERB level.

--==============================================================================
--------------------------------------------------------------------------------
-- § 1.0 - Restriction Level 0
--------------------------------------------------------------------------------
--==============================================================================
THE Level0 IsA level_room.
  NAME 'Restriction Level 0'
  HAS Level 0.
  DESCRIPTION
    "In this level all actions are allowed."
  EXIT west TO Level5. -- circular connections, jump to end!
  EXIT east TO Level1.
END THE Level0.
--==============================================================================
--------------------------------------------------------------------------------
-- § 1.1 - Restriction Level 1
--------------------------------------------------------------------------------
--==============================================================================
THE Level1 IsA level_room.
  NAME 'Restriction Level 1'
  HAS Level 1.
  DESCRIPTION
    "In this level communication verbs are restricted."
  EXIT west TO Level0.
  EXIT east TO Level2.
END THE Level1.
--==============================================================================
--------------------------------------------------------------------------------
-- § 1.2 - Restriction Level 2
--------------------------------------------------------------------------------
--==============================================================================
THE Level2 IsA level_room.
  NAME 'Restriction Level 2'
  HAS Level 2.
  DESCRIPTION
    "In this level all action and communication verbs are restricted."
  EXIT west TO Level1.
  EXIT east TO Level3.
END THE Level2.
--==============================================================================
--------------------------------------------------------------------------------
-- § 1.3 - Restriction Level 3
--------------------------------------------------------------------------------
--==============================================================================
THE Level3 IsA level_room.
  NAME 'Restriction Level 3'
  HAS Level 3.
  DESCRIPTION
    "In this level all in-game verbs are restricted."
  EXIT west TO Level2.
  EXIT east TO Level4.
END THE Level3.
--==============================================================================
--------------------------------------------------------------------------------
-- § 1.4 - Restriction Level 4
--------------------------------------------------------------------------------
--==============================================================================
THE Level4 IsA level_room.
  NAME 'Restriction Level 4'
  HAS Level 4.
  DESCRIPTION
    "In this level no verbs work, not even out-of-game verbs."
  EXIT west TO Level3.
  EXIT east TO Level5.
END THE Level4.
--==============================================================================
--------------------------------------------------------------------------------
-- § 1.5 - Restriction Level 5
--------------------------------------------------------------------------------
--==============================================================================

-- Level 5 is a special restriction level where only YES/NO answers are allowed;
-- it also has a different restriction message ("Please answer YES or NO.".).
-- To test this level we'll have to add a supernatural voice asking a YES/NO
-- question to the player, and different outcomes based on the player reply:

-- Q: "Are you worthy of using the StdLib?"
-- YES: Unlock all verbs by witching to Level 0.
-- NO:  Lock all verbs by witching to Level 4.

-- Exiting and re-entering the room resets it to its initial state.

THE Level5 IsA level_room.
  NAME 'Restriction Level 5'
  HAS Level 5.
  DESCRIPTION
    DEPENDING ON my_game:restricted_level
      = 5 THEN
        "At level 5 you can only answer YES or NO. Also, a special action
         restriction message is shown at this Level, by the library."
      = 4 THEN
        "Since you've enraged the G*d of this room, now He's blocked all actions
         (the room is currently set to Restriction Level 4)."
      = 0 THEN
        "Since the G*d of this room likes you, He's unlocked all actions for you
         (the room is currently set to Restriction Level 0)."
      ELSE
        "**ERROR** Something went wrong!
         (the room is currently set to Restriction Level"
        SAY my_game:restricted_level. ")."
    END DEPEND.

  EXIT west TO Level4.
  EXIT east TO Level0. -- circular connections, back to start!

  ENTERED
    SCHEDULE Level5Event AT Level5 AFTER 0.

  VERB yes
    DOES ONLY
      IF my_game:restricted_level = 5
        THEN
          "Good to hear that!
           As a sign of my mercy I'll unlock all actions in this room for you."
          SET my_game:restricted_level TO 0.
          CANCEL Level5Event.
        ELSE
          "Right now, no one is expecting answers from you."
      END IF.
  END VERB.

  VERB 'no'
    DOES ONLY
      IF my_game:restricted_level = 5
        THEN
          "You ugly ignoramus!
           As a sign of my wrath I shall lock all actions in this room for you."
          SET my_game:restricted_level TO 4.
          CANCEL Level5Event.
        ELSE
          "Right now, no one is expecting answers from you."
      END IF.
  END VERB.
END THE Level5.

--------------------------------------------------------------------------------
-- Level 5 Event
--------------------------------------------------------------------------------
-- Since this special level is tied to YES/NO answers, we'll need an EVENT that
-- asks the player a question. It's triggered on entering the location.

EVENT Level5Event
  IF hero AT Level5 THEN
    "$pA supernatural voice screams at you:
     $n""Mortal, are you worthy of using the StdLib?"""
    SCHEDULE Level5Event AT Level5 AFTER 1.
  END IF.
END EVENT.

--=============================================================================
--\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
--------------------------------------------------------------------------------
-- § 2 - The Hero
--------------------------------------------------------------------------------
--//////////////////////////////////////////////////////////////////////////////
--=============================================================================
-- Let's equip our Hero with a few items to allow tests...

THE cellphone IsA object IN HERO.
  NAME cell phone.
  HAS ex "A cheap cell phone, not the ""smart"" type.".
END THE cellphone.

--------------------------------------------------------------------------------
Start at Level0.
DESCRIBE banner.
"$p~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
 $nEvery room in this test has a different level of actions restriction.
 $nYou can debug the current restriction level using the 'level' verb.
 $n~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~"

-- EOF --
