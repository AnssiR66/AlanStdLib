--==============================================================================
-- "The House" by Tristano Ajmone, 2018-2020.
--==============================================================================
IMPORT 'library.i'. -- ALAN Standard Library v2.2.0


THE my_game IsA DEFINITION_BLOCK

  HAS title    "The House".
  HAS subtitle "A rooms & sites setting for various tests.".
  HAS author   "Tristano Ajmone".
  HAS year     2020.
  HAS version  "1".
  HAS AlanV    "v3.0beta7".

END THE.

-- =============================================================================

-- ROOM: The Kitchen

-- =============================================================================
-- The kitchen will be our testing location for the 'room' class.

THE kitchen IsA room.
  NAME 'The Kitchen'.
  EXIT north TO laundry.
  EXIT south TO garden.
  DESCRIPTION "The laundry room is to the north,
               the south exit leads to your back garden."
END THE kitchen.

-- =============================================================================

-- ROOM: The Laundry Room

-- =============================================================================
-- The laundry will be our testbed for custom descriptions of 'site_object'
-- (walls, floor, ceiling).

THE laundry IsA room.
  NAME 'The Laundry Room'.
  EXIT south TO kitchen.
  DESCRIPTION "The kitchen is to the south."
  HAS walls_desc   "Oh dear! These walls definitely need a fresh paint job.".
  HAS floor_desc   "You can't even remember when you last washed this floor.".
  HAS ceiling_desc "A naked light bulb hangs from the ceiling, strapped to the
                    electric wires with some sticky tape.".

END THE laundry.

-- =============================================================================

-- SITE: The Garden

-- =============================================================================
-- The garden will be our testing location for the 'site' class.
-- We'll hadd custom descriptions for the sky and ground.

THE garden IsA site.
  NAME 'The Back Garden'.
  EXIT north TO kitchen.
  DESCRIPTION "To the north lies your home."
  HAS ground_desc "The lawn is well looked after, mowed as usual.".
  HAS sky_desc    "There are a few dark clouds, maybe it'll rain tonight.".

  -- Since we've excluded the 'chirp' sound from the room description, we'll
  -- make it available at the garden via 'listen':
  VERB listen0
    DOES ONLY "You can ear a faint chirp."
  END VERB listen0.
END THE garden.

-- =============================================================================

-- Kitchen Props

-- =============================================================================

--==============================================================================
-- The Chair
--==============================================================================
-- Let's provide a usable chair.

THE chair IsA supporter AT kitchen.

  VERB sit_on
    DOES ONLY
      IF hero IS lying_down
        THEN "You get up and"
          MAKE hero NOT lying_down.
      END IF.
      "you sit on $+1."
      MAKE hero sitting.
  END VERB sit_on.
END THE chair.

--==============================================================================
-- The Fridge (listed container + openable)
--==============================================================================
THE fridge IsA listed_container AT kitchen.
  OPAQUE CONTAINER -- NOTE: The library doesn't initialize closed listed containers
                   --       to be automatically opaque!
  IS openable.
  IS NOT open.

  INITIALIZE -- Allow the fridge to contain every food item in the game:
    FOR EACH fooditem IsA food
      DO INCLUDE fooditem IN allowed OF THIS.
    END FOR.

END THE fridge.
--==============================================================================
-- The Kitchen Table (supported)
--==============================================================================

THE kitchen_table IsA supporter AT kitchen.
  IS NOT takeable.
  NAME table.
END THE kitchen_table.

--==============================================================================
-- Fruits Basket (listed container on table)
--==============================================================================

THE basket IsA listed_container IN kitchen_table.

END THE basket.

--==============================================================================
-- Shopping List (readable object)
--==============================================================================
-- It has no 'ex' description, so READ and EXAMINE behave the same.
THE shopping_list IsA object IN kitchen_table.
  NAME shopping 'list'.
  IS readable.
  HAS text "Don't forget to buy: milk, flour, eggs, yeast. (for the cake!)".
END THE shopping_list.

--==============================================================================
-- Postcard (readable object with 'ex')
--==============================================================================
-- It has an 'ex' description, to test EXAMINE vs READ.
THE postcard IsA object IN kitchen_table.
  NAME postcard. NAME card.
  IS readable.
  HAS ex "The postcard front picture depicts Paris at night, with the
          illuminated Eiffel Tower in the background.".
  HAS text
    "Paris is sooooo cool and romantic! Love, Norma.".
END THE postcard.

-- =============================================================================

-- Food & Beverages (Edibles & Drinkables)

-- =============================================================================
-- We'll create some 'edible' and 'drinkable' object classes and instances to
-- allow testing with solids and liquids. Hierarchical subclassing will allow us
-- to easily set which kind of food can be put where, and also provide a mean to
-- test disambiguation.
--==============================================================================
-- Food Classes
--==============================================================================
EVERY food IsA object.
  NAME food.
  IS edible.
END EVERY.

EVERY beverage IsA liquid.
  IS drinkable.
  NAME beverage.
END EVERY.

EVERY snack IsA food.
  NAME snack.
END EVERY.
--------------------------------------------------------------------------------
-- Food Subclasses
--------------------------------------------------------------------------------
EVERY fruit IsA food.
  NAME fruit.
END EVERY fruit.
--------------------------------------------------------------------------------
-- Beverage Vessels
--------------------------------------------------------------------------------
-- Let's create a specialized class for beverage containers, to simplify pouring
-- liquids from one container to another...
EVERY beverage_vessel IsA listed_container.

  INITIALIZE -- Allow every beverage vessel to contain every beverage in the game:
    FOR EACH bevitem IsA beverage
      DO INCLUDE bevitem IN allowed OF THIS.
    END FOR.
END EVERY beverage_vessel.
--------------------------------------------------------------------------------
-- Snacks Containers
--------------------------------------------------------------------------------
-- Snacks containers are small packages that can take small snacks (chips, olives,
-- etc.) but not big food items (like fruits, etc.). Useful for testing responses
-- of verbs that allow putting things in containers.

EVERY snack_cont IsA listed_container.

  INITIALIZE -- Allow every snack container to contain every snack in the game:
    FOR EACH snackitem IsA snack
      DO INCLUDE snackitem IN allowed OF THIS.
    END FOR.
END EVERY snack_cont.

--==============================================================================
-- FOOD INSTANCES: Fruits
--==============================================================================
-- Let's put some fruits in the fruit basket.

THE apple IsA fruit IN basket.
  NAME apple.
END THE.

THE banana IsA fruit IN basket.
  NAME banana.
END THE.

THE pear IsA fruit IN basket.
  NAME pear.
END THE.
--==============================================================================
-- BEVERAGE INSTANCES
--==============================================================================

--------------------------------------------------------------------------------
-- Empty Jar
--------------------------------------------------------------------------------
THE jar IsA beverage_vessel IN kitchen_table.
  HAS allowed {wine}.
END THE.
--------------------------------------------------------------------------------
-- Wine Bottle
--------------------------------------------------------------------------------
THE bottle IsA beverage_vessel IN kitchen_table.
END THE.

THE wine IsA liquid IN bottle.
  HAS vessel bottle.
  INDEFINITE ARTICLE "some"
END THE.
--==============================================================================
-- SNACKS INSTANCES
--==============================================================================
-- TODO: implemt it as a once-only openable container (ie, once open can't be
--       closed again).
THE chips_bag IsA snack_cont IN kitchen_table.
  NAME 'chips bag'.
  NAME bag.
END THE chips_bag.

THE chips IsA snack IN chips_bag.
  INDEFINITE ARTICLE "some"
END THE chips.

-- =============================================================================

-- Garden Props

-- =============================================================================

--==============================================================================
-- Bird's Chirp
--==============================================================================
-- Let's add a 'sound' to test 'listen' and to also provide an object which is
-- NOT examibable, takeable, reachable or movable.
THE chirp IsA sound AT garden.
  DESCRIPTION ""  -- We don't want it to show up in the room description
                  -- (it'll show up when listening in the garden via listen0).

  VERB listen
    DOES ONLY
      IF hero AT obj
        THEN "It sounds like a sparrow's chirp."
      ELSIF obj NEAR hero
        THEN "From this distance it's difficoult to tell which bird it might be."
      END IF.
  END VERB listen.
END THE chirp.

--==============================================================================
-- The Garden's Table
--==============================================================================
THE garden_table IsA supporter AT garden.
  IS NOT takeable.
  NAME table.
END THE garden_table.

--==============================================================================
-- Folding Bed
--==============================================================================
THE garden_bed IsA supporter AT garden.
  NAME folding bed.
  NAME bed.
  HAS ex "It's a folding bed, ideal for beaches and camping.".

  VERB lie_on
    DOES ONLY
      IF hero IS sitting
        THEN "You get up and"
          MAKE hero NOT sitting.
      END IF.
      "you lie down on $+1."
      MAKE hero lying_down.
  END VERB lie_on.
END THE garden_bed.

--==============================================================================
-- The Swimming Pool
--==============================================================================
-- Let's create a swimming pool to test both 'swim' and 'enter'.

THE swimming_pool IsA liquid AT garden.
  NAME swimming pool.
  NAME water.

  VERB swim
    DOES ONLY
      IF hero DIRECTLY AT inside_pool
        THEN "You are already in the swimming pool."
        ELSE
          LOCATE hero AT inside_pool.
      END IF.
  END VERB swim.

  VERB swim_in
    DOES ONLY
      IF hero DIRECTLY AT inside_pool
        THEN "You are already in the swimming pool."
        ELSE
          LOCATE hero AT inside_pool.
      END IF.
  END VERB swim_in.

  VERB enter
    DOES ONLY
      IF hero DIRECTLY AT inside_pool
        THEN "You are already in the swimming pool."
        ELSE
          LOCATE hero AT inside_pool.
      END IF.
  END VERB enter.

  VERB 'exit'
    DOES ONLY
      IF hero NOT DIRECTLY AT inside_pool
        THEN "You are not inside the swimming pool."
        ELSE
          "You climb over the pool border and get back on the grass."
          LOCATE hero AT garden.
      END IF.
  END VERB 'exit'.
END THE swimming_pool.

--==============================================================================
-- Inside The Swimmin Pool
--==============================================================================
-- To simulate the hero entering the pool we create a nested location AT the
-- garden.

THE inside_pool IsA location AT garden.
  NAME 'Inside The Swimming Pool (in Garden)'.
  DESCRIPTION
    "The pool is wide enough to enjoy swimming in it."

  ENTERED
    "You dive savagely into the pool, splashing water everywhere."

END THE inside_pool.

--------------------------------------------------------------------------------
Start at kitchen.
DESCRIBE banner.
