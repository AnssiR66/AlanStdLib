--==============================================================================
-- "Emporium Giorgio Alani" by Tristano Ajmone, 2018.
--==============================================================================
-- A small adventure for testing the StdLib's clothing functionality and the
-- predefined verb reponses.

-- Released into the public domain via the Unlicense:
--    http://unlicense.org/
--------------------------------------------------------------------------------
-- WIP NOTE: This file is still work-in-progress, and doesn't yet test all the
--           commands and contexts related to clothing.
--------------------------------------------------------------------------------
IMPORT 'library.i'.      -- ALAN Standard Library v2.1
IMPORT '../inc_debug.i'. -- Debugging Module (adds useful test verbs)


THE my_game IsA DEFINITION_BLOCK

  HAS title    "Emporium Giorgio Alani".
  HAS subtitle "An adventure to test clothing.".
  HAS author   "Tristano Ajmone".
  HAS year     2018.
  HAS version  "1".
  HAS AlanV    "v3.0beta6 build 1880".

END THE.

--==============================================================================
-- LOCATIONS TWEAKS
--==============================================================================

-- Let's add to all locations some custom attributes:

-- 1. A string attribute with an alternative room name to show when referencing
--    the room in custom output. Usally a shorter and lowercase version of the
--    location printed name. Currently used in the 'Reactivate_Assistant' EVENT
--    to mention where she came from (if the string is empty, 'LOCATION OF' will
--    be used instead).

-- 2. A string attribute to store the list of exits of a location. Currently
--    used by the EXITS verb to print the exits on demand.

ADD TO EVERY location
  HAS room_alt_name "". -- (optional) alternative name to show in events.
  HAS room_exits "".    -- (optional) text listing the location exits.
END ADD TO location.


--==============================================================================
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--------------------------------------------------------------------------------
--
-- THE WORLD MAP
--
--------------------------------------------------------------------------------
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--==============================================================================

-- Let's sketch out the world map rooms, we'll deal with props and furniture
-- further on...

--         +-------+-------+   +-------+-------+   +-------+-------+
--         |   WORKSWEAR   |---|      ???      |---|      ???      |
--         +-------+-------+   +-------+-------+   +-------+-------+
--                 |        \          |          /        |
--                 |         \         |         /         |
--                 |          \        |        /          |
--         +-------+-------+   +-------+-------+   +-------+-------+
--         |  SPORTSWEAR   |---| DRESSING ROOM |---|   COSTUMES    |
--         +-------+-------+   +-------+-------+   +-------+-------+
--                 |          /        |        \          |
--                 |         /         |         \         |
--                 |        /          |          \        |
--         +-------+-------+   +-------+-------+   +-------+-------+
--         |  WOMENSWEAR   |---|   MAIN HALL   |---|   MENSWEAR    |
--         +---------------+   +---------------+   +---------------+



-- =============================================================================

-- Emporium GA Entrance

-- =============================================================================
-- We provide a street connection to the emporium so that later on we can move
-- the emporium into the main test adventure instead of having separate files.

THE ega_entrance IsA site.
  NAME 'Outside Emporium Alani'.
  EXIT north TO ega_hall
    DOES
      ----------------
      -- Sliding Doors
      ----------------
      SAY ega_doors:traverse_txt.
      LOCATE ega_doors AT ega_hall.
      LOCATE ega_doorframe AT ega_hall.
      ---------------------
      -- Personal Assistant
      ---------------------
      MAKE personal_assistant following.
  END EXIT.
  DESCRIPTION
    "You're standing in front of the Giorgio Alani Emporium entrance."
  HAS room_exits "NORTH into the Alan store.".
END THE ega_entrance.
--==============================================================================

-- The Main Hall

--==============================================================================
THE ega_hall IsA room.
  NAME 'Emporium Alani Main Hall'.
  HAS room_alt_name "main hall".
  EXIT east  TO ega_menswear.
  EXIT west  TO ega_womenswear.
  EXIT north TO dressing_room.
  EXIT south TO ega_entrance
    DOES
      ----------------
      -- Sliding Doors
      ----------------
      SAY ega_doors:traverse_txt.
      LOCATE ega_doors AT ega_hall.
      LOCATE ega_doorframe AT ega_hall.
      ---------------------
      -- Personal Assistant
      ---------------------
      MAKE personal_assistant NOT following.
      "The assistant greets your farewell as you leave."
  END EXIT.
  DESCRIPTION
    "This luxurious hall is the crossroad to the various clothing departments of
     the emporium."
  HAS room_exits
    "EAST to Menswear dept., WEST to Womenswear dept., and
     NORTH to the dressing room.".
END THE ega_hall.

-- =============================================================================

-- The Dressing Room

-- =============================================================================
-- We'll place the drssing room at the center of the EGA square, so it can be
-- accessed from all departments.

THE dressing_room IsA room.
  NAME 'The Dressing Room'.
  EXIT south TO ega_hall
    DOES SCHEDULE Reactivate_Assistant AT hero AFTER 0.
  END EXIT.
  EXIT southeast TO ega_menswear
    DOES SCHEDULE Reactivate_Assistant AT hero AFTER 0.
  END EXIT.
  EXIT east TO ega_costumes
    DOES SCHEDULE Reactivate_Assistant AT hero AFTER 0.
  END EXIT.
  EXIT northeast TO ega_free2
    DOES SCHEDULE Reactivate_Assistant AT hero AFTER 0.
  END EXIT.
  EXIT north TO ega_free1
    DOES SCHEDULE Reactivate_Assistant AT hero AFTER 0.
  END EXIT.
  EXIT northwest TO ega_workwear
    DOES SCHEDULE Reactivate_Assistant AT hero AFTER 0.
  END EXIT.
  EXIT west TO ega_sportswear
    DOES SCHEDULE Reactivate_Assistant AT hero AFTER 0.
  END EXIT.
  EXIT southwest TO ega_womenswear
    DOES SCHEDULE Reactivate_Assistant AT hero AFTER 0.
  END EXIT.
  DESCRIPTION
    "The curtains surrounding this room provide you all the privacy you need to
     disrobe yourself and try on clothing."
  HAS room_exits
    "NORTH to Dept. 1, NORTHEAST to Dept. 2,
     EAST to Costumes,
     SOUTHEAST to Menswear,
     SOUTH to the hall,
     SOUTHWEST to Womenswear,
     WEST to Sportswear, and
     NORTHWEST to Workswear.".
  VERB undress -- Allow undressing action when in Dressing Room:
    DOES ONLY
      IF COUNT IsA clothing, DIRECTLY IN hero, IS worn > 0
        THEN
          "You remove all the items you were wearing."
          FOR EACH worncl IsA clothing, DIRECTLY IN hero, IS worn
            DO
              MAKE worncl NOT worn.
              LOCATE worncl AT CURRENT LOCATION.
          END FOR.
        ELSE "You're not wearing anything you can remove."
      END IF.
  END VERB undress.
END THE dressing_room.

-- =============================================================================

-- The Mensware Department

-- =============================================================================
THE ega_menswear IsA room.
  NAME 'Mensware Department'.
  HAS room_alt_name "mensware dept.".
  EXIT west      TO ega_hall.
  EXIT north     TO ega_costumes.
  EXIT northwest TO dressing_room.
  DESCRIPTION
    "Everything a man would ever dream wearing can be found here."
  HAS room_exits
    "NORTH to Costumes, NORTHWEST to the dressing room, and WEST to the hall.".
  END THE ega_menswear.

-- =============================================================================

-- The Womensware Department

-- =============================================================================
THE ega_womenswear IsA room.
  NAME 'Womensware Department'.
  HAS room_alt_name "womensware dept.".
  EXIT east      TO ega_hall.
  EXIT north     TO ega_sportswear.
  EXIT northeast TO dressing_room.
  DESCRIPTION
    "No woman's fashion dreams could be left unfulfilled by the clothes in this
     department."
  HAS room_exits
    "NORTH to Sportswear, NORTHEAST to the dressing room, and EAST to the hall.".
END THE ega_womenswear.

-- =============================================================================

-- The Costumes Department

-- =============================================================================
THE ega_costumes IsA room.
  NAME 'Costumes Department'.
  HAS room_alt_name "costumes dept.".
  EXIT south TO ega_menswear.
  EXIT west  TO dressing_room.
  EXIT north TO ega_free2.
  DESCRIPTION
    "All around you, you can see costume for any occasion that comes to mind."
  HAS room_exits
    "SOUTH to Mensware, NORTH to Dept. 2, and WEST to the dressing room.".
END THE ega_costumes.

-- =============================================================================

-- The Sportswear Department

-- =============================================================================
THE ega_sportswear IsA room.
  NAME 'Sportswear Department'.
  HAS room_alt_name "sportswear dept.".
  EXIT north TO ega_workwear.
  EXIT south TO ega_womenswear.
  EXIT east  TO dressing_room.
  DESCRIPTION
    "Can't think of any sport that you couldn't find gear for in this room."
  HAS room_exits
    "NORTH to Workswear, SOUTH to Womensware, and EAST to the dressing room.".
END THE ega_sportswear.


-- =============================================================================

-- The Workwear Department

-- =============================================================================
THE ega_workwear IsA room.
  NAME 'Workwear and Safety Department'.
  HAS room_alt_name "workwear dept.".
  EXIT south      TO ega_sportswear.
  EXIT east       TO ega_free1.
  EXIT southeast  TO dressing_room.
  DESCRIPTION
    "Safety first, for every profession you'll find the right workwear here."
  HAS room_exits
    "SOUTH to Sportswear, EAST to Dept. 1, and SOUTHEAST to the dressing room.".
END THE ega_workwear.

-- =============================================================================

-- Department 1 (Unassigned)

-- =============================================================================
THE ega_free1 IsA room.
  NAME 'Department #1 (empty)'.
  HAS room_alt_name "Dept #1".
  EXIT south TO dressing_room.
  EXIT east  TO ega_free2.
  EXIT west  TO ega_workwear.
  DESCRIPTION
    "This department is currently unassigned, its showcases empty and awaiting."
  HAS room_exits
    "EAST to Dept. 2, WEST to Workwear, and SOUTH to the dressing room.".
END THE ega_free1.


-- =============================================================================

-- Department 2 (Unassigned)

-- =============================================================================
THE ega_free2 IsA room.
  NAME 'Department #2 (empty)'.
  HAS room_alt_name "Dept #2".
  EXIT southwest TO dressing_room.
  EXIT west      TO ega_free1.
  EXIT south     TO ega_costumes.
  DESCRIPTION
    "This department is currently unassigned, its showcases empty and awaiting."
  HAS room_exits
    "SOUTH to Costumes, WEST to Dept. 1, and SOUTHWEST to the dressing room.".
END THE ega_free2.


--==============================================================================
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--------------------------------------------------------------------------------
--
-- SPECIALIZED CLASSES
--
--------------------------------------------------------------------------------
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--==============================================================================

-- Let's create some specialized classes to simplify our coding life...

--=============================================================================
--\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
--------------------------------------------------------------------------------
-- CLOTHING CLASSES
-------------------------------------------------------------------------------
--//////////////////////////////////////////////////////////////////////////////
--=============================================================================

-- Let's create some clothes classes to simplify the task of creating multiple
-- clothing items of the same kind...

-- All clothing classes name start with the 'cl_' prefix.
-- We'll be creating two types of clothing subclasses:
--
--   (1) Prototypes -- 'proto_' prefix.
--   (2) General purpose classes -- 'cl_' prefix.

-- >>> FIXME >>> EGA Clothing Table
--     +-----------------------------------------------------------------------+
--     | Since the new clothing system doesn't enforce any more exponential    |
--     | layer values, I shuould customize these values according to needs.    |
--     +-----------------------------------------------------------------------+

-- Custom Clothing Table
--+--------------------------+----------+----------+-----------+------------+
--| Clothing     | Headcover | Topcover | Botcover | Feetcover | Handscover |
--+--------------------------+----------+----------+-----------+------------+
--| hat                    2 |        0 |        0 |         0 |          0 |
--| vest/bra               0 |        2 |        0 |         0 |          0 |
--| undies/panties         0 |        0 |        2 |         0 |          0 |
--| teddy                  0 |        4 |        4 |         0 |          0 |
--| blouse/shirt/T-shirt   0 |        8 |        0 |         0 |          0 |
--| dress/coveralls        0 |        8 |       32 |         0 |          0 |
--| skirt                  0 |        0 |       32 |         0 |          0 |
--| trousers/shorts        0 |        0 |       16 |         0 |          0 |
--| sweater/pullover       0 |       16 |        0 |         0 |          0 |
--| jacket                 0 |       32 |        0 |         0 |          0 |
--| coat                   0 |       64 |       64 |         0 |          0 |
--| socks/stockings        0 |        0 |        0 |         2 |          0 |
--| tights/pantiehose      0 |        0 |        8 |         2 |          0 |
--| shoes/boots            0 |        0 |        0 |         4 |          0 |
--| gloves                 0 |        0 |        0 |         0 |          2 |
--+--------------------------+----------+----------+-----------+------------+

--==============================================================================
-- HEADWEAR & FACEWEAR
--==============================================================================
-- Hats will cover layer 2 of the head, layer 1 being reserved for balaclavas
-- which are in direct skin contact and can be worn underneath hats, helmets, etc..

EVERY proto_headwear IsA clothing.
  HAS headcover 2.
END EVERY.

EVERY cl_hat IsA proto_headwear.
  NAME hat.
END EVERY.

--==============================================================================
-- UNDERWEAR
--==============================================================================
EVERY proto_underwear IsA clothing.
  NAME underwear.
END EVERY.
--------
-- Chest
--------
EVERY cl_undershirt IsA proto_underwear.
  NAME undershirt.
  HAS topcover 2.
END EVERY.
---------
-- Pelvis
---------
EVERY cl_underpants IsA proto_underwear.
  NAME underpants.
  HAS botcover 2.
  INDEFINITE ARTICLE "a pair of"
END EVERY.
-------
-- Feet
-------
EVERY cl_socks IsA clothing.
  NAME socks.
  HAS feetcover 2.
  INDEFINITE ARTICLE "a pair of"
END EVERY.

--==============================================================================
-- FOOTWEAR
--==============================================================================
EVERY cl_shoes IsA clothing.
  NAME shoes.
  HAS feetcover 4.
  INDEFINITE ARTICLE "a pair of"
END EVERY.

EVERY cl_boots IsA clothing.
  NAME boots.
  IS plural.
  HAS feetcover 4.
  INDEFINITE ARTICLE "a pair of"
END EVERY.

--==============================================================================
-- SHIRTS & OTHER 'Topcover 8' CLOTHES
--==============================================================================
EVERY proto_shirt IsA clothing.
  HAS topcover 8.
END EVERY.

EVERY cl_tshirt IsA proto_shirt.
  NAME 'T-shirt'.
  NAME tshirt.
  NAME tee.
END EVERY.

--==============================================================================
-- SWEATERS & PULLOVERS
--==============================================================================

EVERY proto_pullover IsA clothing.
  HAS topcover 16.
END EVERY.


--==============================================================================
-- LEGWEAR
--==============================================================================

EVERY proto_skirt IsA clothing.
  NAME skirt.
  HAS botcover 32. NOT blockslegs. --> Skirts don't prevent wearing lower legswear!
END EVERY.

EVERY proto_trousers IsA clothing.
  NAME trousers.
  IS plural.
  INDEFINITE ARTICLE "a pair of"
  HAS botcover 16.
END EVERY.

EVERY proto_pantyhose IsA clothing.
  NAME pantyhose.
  IS plural.
  INDEFINITE ARTICLE "a pair of"
  HAS botcover 8.
END EVERY.


--==============================================================================
-- COVERALLS & OVERALLS
--==============================================================================

EVERY proto_coverall IsA clothing.
  NAME coverall.
  NAME coveralls.
  HAS topcover 8.
  HAS botcover 32.
END EVERY.

EVERY proto_overall IsA clothing.
  NAME overall.
  NAME overalls.
  HAS topcover 8.
  HAS botcover 32.
END EVERY.

--==============================================================================
-- DRESS
--==============================================================================

EVERY proto_dress IsA clothing.
  NAME dress.
  HAS topcover 8.
  HAS botcover 32. NOT blockslegs. --> Skirts don't prevent wearing lower legswear!
END EVERY.

--==============================================================================
-- COATS
--==============================================================================

EVERY cl_coat IsA clothing.
  NAME coat.
  HAS topcover 64.
  HAS botcover 64. NOT blockslegs. --> Coats don't prevent wearing lower legswear!
END EVERY.

--==============================================================================
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--------------------------------------------------------------------------------
--
-- CUSTOM VERBS
--
--------------------------------------------------------------------------------
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--==============================================================================

--==============================================================================
-- LIST EXITS
--==============================================================================

-- The list of exits in most rooms is too long (and hugly) to be shown in the
-- description, so I've added a new 'room_exits' attribute on every location to
-- store a string with the exits lists, which will be shown  on demand via the
-- EXITS verb.

-- TODO: Find an easy way to print room_exits the first time a room is visited.

SYNTAX exits = exits.

VERB exits
  DOES
    IF room_exits OF CURRENT LOCATION <> ""
      THEN "You can go:" SAY room_exits OF CURRENT LOCATION.
      ELSE "Sorry, there isn't an exits list available for this location."
    END IF.
END VERB exits.

--==============================================================================
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--------------------------------------------------------------------------------
--
-- ROOMS FURNITURE & PROPS
--
--------------------------------------------------------------------------------
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--==============================================================================

-- Time to fill the various rooms with props and stuff...

--=============================================================================
--\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
--------------------------------------------------------------------------------
-- FURNITURE & PROPS: Emporium GA Entrance
-------------------------------------------------------------------------------
--//////////////////////////////////////////////////////////////////////////////
--=============================================================================

--==============================================================================
-- Emporium Doors
--==============================================================================
-- These fake doors are automatic-opening doors. They are moved from the emporium
-- entrance to the main hall when the hero wals in and out of it, to avoid having
-- recreate them inside too.

THE ega_doors IsA object AT ega_entrance.
  NAME 'emporium doors'.
  NAME doors.
  DESCRIPTION
    IF THIS AT ega_entrance
      THEN
        "Two large brass doors await northward your entrance into the sanctuary
         of fashion consumism."
      ELSE
        "Two large brass doors lead the way south, out of the fashion temple and
         back into the world of mortal souls."
    END IF.
  NAME door.
  HAS ex "Engraved on the large brass doors the striped eagle of the ""GA"" logo
          stares down at you like a g*d upon his humble servant.
          There are no handles on doors, but you notice a small sensor just above
          the doorframe".
  IS NOT takeable.
  IS NOT movable.
  HAS traverse_txt
    "As you move toward the doors they automagically slide open before you.".
END THE ega_doors.

THE ega_doorframe IsA object AT ega_entrance.
  NAME doorframe.
  NAME door frame.
  NAME sensor.
  MENTIONED "the doorframe and the sensor"
  IS scenery.
  IS plural.
END THE ega_doorframe.


--=============================================================================
--\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
--------------------------------------------------------------------------------
-- FURNITURE & PROPS: Main Hall
-------------------------------------------------------------------------------
--//////////////////////////////////////////////////////////////////////////////
--=============================================================================

-- Having a surface to carry out tests in the Hall is convenient:

THE desk IsA supporter AT ega_hall.
  DESCRIPTION "" -- We'll describe the desk along with the manager.
  HAS ex
    "This luxurios mahogany desk is where customers go and pay their purchased
     merchandise, a bit like the supermarket cash, but more discrete.".
  VERB look_behind
    DOES ONLY
      "Peeking behind the desk you notice a gun, hidden out of sight but readily
       grabbable if the need comes. They must keep it there for emergencies."
      LOCATE gun AT ega_hall.
  END VERB look_behind.
END THE desk.

-- In orders to test verbs that need a weapon, we'll hide a gun behind the desk,
-- which will become visible after looking behind the desk.

THE gun IsA weapon.
  NAME gun.
  NAME pistol.
  CAN shoot.
  IS hidden.
  MENTIONED
    "gun"
    IF THIS IS hidden
      THEN "(hidden behind the desk)"
    END IF.
  HAS ex "A semiautomatic pistol, seemingly loaded.".

  VERB take
    DOES AFTER
      MAKE THIS NOT hidden.
  END VERB take.
END THE gun.


THE trashcan IsA listed_container AT ega_hall.
  NAME trashcan.
  NAME bin.
  WITH CONTAINER
    HEADER "Inside the trashcan you can see"
  IS NOT moveable. NOT takeable.
  HAS ex "Just a trashcan.".

  INITIALIZE
    -- Let's ensure that the trashcan can contain any object of the game:
    FOR EACH obj IsA object
      DO INCLUDE obj IN allowed OF THIS.
    END FOR.
  
  VERB examine
    DOES ONLY
      LIST THIS.
  END VERB examine.
END THE.

--=============================================================================
--\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
--------------------------------------------------------------------------------
-- FURNITURE & PROPS: The Dressing Room
-------------------------------------------------------------------------------
--//////////////////////////////////////////////////////////////////////////////
--=============================================================================

THE chair IsA object AT dressing_room.
  HAS ex "It's a plastic chair.".
END THE chair.


THE basket IsA listed_container AT dressing_room.
  HAS ex "It's a wicker basket where you can drop discarded clothing items.".
  INITIALIZE
    -- We must allow the basket to contain any clothing item:
    FOR EACH cl ISA clothing
      DO INCLUDE cl IN allowed OF THIS.
    END FOR.
END THE basket.


--==============================================================================
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--------------------------------------------------------------------------------
--
-- NPC ACTORS
--
--------------------------------------------------------------------------------
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--==============================================================================

--==============================================================================
--------------------------------------------------------------------------------
-- The Manager
--------------------------------------------------------------------------------
--==============================================================================

-- We'll place this NPC in the Main Hall, so that we might test those verbs
-- that would transfer (implicitly) worn clothing from one NPC to another.

THE manager IsA male AT ega_hall.
  DESCRIPTION "Behind a desk stands the emporium manager."
END THE manager.

--==============================================================================
-- Abbigliamento Manager
--==============================================================================
THE manager_trousers IsA proto_trousers IN manager.
  IS worn.
  NAME pleated trousers.
  NAME his pleated.
END THE.

THE manager_pullover IsA proto_pullover IN manager.
  IS worn.
  NAME pullover.
  NAME his pullover.
END THE.

THE manager_shoes IsA cl_shoes IN manager.
  IS worn.
  NAME loafers.
END THE.

--==============================================================================
--------------------------------------------------------------------------------
-- The Personal Assistant
--------------------------------------------------------------------------------
--==============================================================================
-- This NPC is intended not just a simple clerk but as "personal assistant"
-- thuroughout the whole visit to the Emporium.
-- It should be automated to carry out some basic chores:
--   [x] follow the hero inside the emporium, but:
--       [x] not leave the Emporium when the Hero does.
--       [ ] not enter the Dressing Room.
-- (Still need to implement some of these.)

-- Since the EGA has a single entrance/exit, the Assistant 'following' can be
-- set and unset via EXITs.

THE personal_assistant IsA female AT ega_hall.
  NAME assistant.
  NAME personal assistant.
  INDEFINITE FORM "your personal assistant"
  HAS ex "She's your personal shopping assistant.".
  
  INITIALIZE
    -- ---------------------------------------------------------
    -- Turn every clothing IN assistant into an item worn by her
    -- ---------------------------------------------------------
    FOR EACH cl IsA clothing, IN THIS
      DO MAKE cl worn.
    END FOR.
END THE personal_assistant.

-- Prevent following hero in Dressing Room:
WHEN hero AT dressing_room
  => MAKE personal_assistant NOT following.
END WHEN.

-- To reactivate following we use this event, triggered by Dressing Room EXITS:
EVENT Reactivate_Assistant
  -- If the hero exits the dressing room to a different location than the one
  -- he entered from then the assistant will need to catch up with him:
  IF personal_assistant NOT AT hero
    THEN
      "The assistant comes running into the room, all the way from the"
      IF room_alt_name OF location of personal_assistant <> ""
        THEN SAY room_alt_name OF location of personal_assistant.
        ELSE SAY location OF personal_assistant.
      END IF.
      ", where you left her."
      LOCATE personal_assistant AT hero.
  END IF.
  MAKE personal_assistant following.
END EVENT.

--==============================================================================
-- Personal Assistant Clothing
--==============================================================================
-- We'll dress her with some clothing items so that we can test how worn items
-- are shown when she's examined, and how various vanilla library verbs deal
-- with her worn clothing (ask, take, etc.).

THE assistant_uniform IsA proto_dress IN personal_assistant.
  NAME uniform.
  NAME assistant dress.
  NAME assistant uniform.
  HAS ex "Her uniform is a one piece dress, elegant and professional.".
END THE.

THE assistant_shoes IsA cl_boots IN personal_assistant.
  NAME fashion boots.
  NAME assistant boots.
  HAS ex "A pair of high-heeled knee-high black leather fashion boots.".
END THE.


--==============================================================================
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--------------------------------------------------------------------------------
--
-- CLOTHING ITEMS
--
--------------------------------------------------------------------------------
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--==============================================================================

-- Now let's create the actual clothing items and place them in the various
-- store departments...


--==============================================================================
-- The Dressing Room (temporary)
--==============================================================================
-- For the sake of initial testing, some clothing items will be located in the
-- Dressing Room, temporarily...

THE dungarees IsA proto_overall AT dressing_room.
  NAME dungarees.
  IS plural.
  INDEFINITE ARTICLE "a pair of"
  HAS ex "It's a bib-and-brace overalls, fashioned out of blue denim.".
END THE.

THE red_skirt IsA proto_skirt AT dressing_room.
  HAS ex "It's a red skirt.".
END THE.

THE white_pantyhose IsA proto_pantyhose AT dressing_room.
  NAME white pantyhose.
  HAS ex "It's a pair of white nylon pantyhose.".
END THE.

THE blue_jeans IsA proto_trousers AT dressing_room.
  NAME jeans.
  NAME blue jeans.
  HAS ex "It's a pair of blue jeans.".
END THE.

--=============================================================================
--\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
--------------------------------------------------------------------------------
-- Mensware Department Clothing
-------------------------------------------------------------------------------
--//////////////////////////////////////////////////////////////////////////////
--=============================================================================

THE tshirt IsA cl_tshirt AT ega_menswear.
END THE.

THE black_shirt IsA cl_tshirt AT ega_menswear.
  NAME black shirt.
END THE.

THE red_shirt IsA cl_tshirt AT ega_menswear.
  NAME red shirt.
END THE.

THE man_coat IsA cl_coat AT ega_menswear.
  NAME 'men''s coat'.
  NAME men man coat.
END THE.



--=============================================================================
--\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
--------------------------------------------------------------------------------
-- Sportsware Department Clothing
-------------------------------------------------------------------------------
--//////////////////////////////////////////////////////////////////////////////
--=============================================================================



--==============================================================================
--------------------------------------------------------------------------------
-- Winter Sports Gear
--------------------------------------------------------------------------------
--==============================================================================

-- A balaclava covers both face and head, and it's in direct skin contact.

THE balaclava IsA clothing AT ega_sportswear.
  HAS headcover 1.
  HAS facecover 1.
  HAS ex
    "It's a cotton balaclava for winter sports. Basically, a head hood with holes
     for the eyes and mouth, like the ones used in robberies.".
END THE.

THE ski_goggles IsA clothing AT ega_sportswear.
  NAME ski goggles.
  IS plural.
  HAS facecover 2.
  INDEFINITE ARTICLE "a pair of"
  HAS ex
    "A pair of goggles for skiing, with an elastic band to hold them in place.
     Its front B-shaped single lens has a special coating adaptative to light.".
END THE.

THE ski_helmet IsA clothing AT ega_sportswear.
  NAME ski helmet.
  HAS headcover 2.
  HAS ex
    "It's a hard plastic helmet covering head and ears, with soft padding inside.
     A chinstrap allows securing it firmly in place.".
END THE.



--==============================================================================
--------------------------------------------------------------------------------
-- Swimming Gear
--------------------------------------------------------------------------------
--==============================================================================


--------------------------------------------------------------------------------
-- One-Piece Swimsuit
--------------------------------------------------------------------------------
-- The one-piece swimsuit is like a teddy for it covers both legs and torso in
-- a single piece; so it's useful for testing skirts.

THE onepiece_swimsuit IsA clothing AT ega_sportswear.
  NAME swimsuit.
  HAS topcover 2.
  HAS botcover 2.
  HAS ex "A one-piece swimsuit, for women swimmers.".
END THE.

--------------------------------------------------------------------------------
-- Bikini (two pieces leg + torso)
--------------------------------------------------------------------------------
-- The bikini is a special item for it covers both legs and torso but in two
-- separate pieces, although it's treated as a single item.

THE bikini IsA clothing AT ega_sportswear.
  HAS topcover 2.
  HAS botcover 2. IS twopieces. --> legs and torso are covered separately
  HAS ex "A two-piece swimsuit, for women.".
END THE.


--==============================================================================
-- VALUELESS ITEMS (ORNAMENTALS)
--==============================================================================
-- Some wearables that don't have any coverage values and can therefore be worn
-- freely, without blocking checks...

THE sports_watch IsA clothing AT ega_sportswear.
  NAME sports watch.
  HAS ex
    "A digital watch with all sorts of apps to monitor the heart, measure the
     temperature, geo-localize, etc.".
END THE.

--------------------------------------------------------------------------------
-- Scarfs
--------------------------------------------------------------------------------
THE white_scarf IsA clothing AT ega_sportswear.
  NAME white scarf.
  HAS ex "Just a white wool scarf.".
END THE.

THE blue_scarf IsA clothing AT ega_sportswear.
  NAME blue wool scarf.
  HAS ex "Just a blue wool scarf.".
END THE.

THE black_scarf IsA clothing AT ega_sportswear.
  NAME black scarf.
  HAS ex "A black wool scarf.".
END THE.




--==============================================================================
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--------------------------------------------------------------------------------
--
-- THE HERO
--
--------------------------------------------------------------------------------
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--==============================================================================

-- Let's provide our hero with some clothing of its own to start with (we can't
-- expect him to walk into Alani's emporium naked, right?).

-- TODO:
-- [ ] Add an attribute to distinguish hero original clothes from Emporium items,
--     so that the assistant can hold the former for the hero while he's trying
--     the latter. Also, the assistant will be putting back dropped emporium
--     clothes to keep the place tidy.

--==============================================================================
-- CLOTHING
--==============================================================================

-- TODO:
-- [ ] Could use an 'owned' attribute to allow INITIALIZE all hero clothing to
--     have same ARTICLE and place them IN WORN, and slim down code here.
-- [ ] Order of creation matters: items will be listed in inverse order.

THE hero_socks IsA cl_socks IN hero.
  IS worn.
  NAME socks.
  NAME my white socks.
  INDEFINITE ARTICLE "your"
  DEFINITE ARTICLE "your"
  HAS ex
    "A pair of plain white socks turned nicotine yellow in the course of time.
     The right sock has a hole big enough for your toe to pop out.".
END THE hero_socks.

THE hero_boxers IsA cl_underpants IN hero.
  IS worn.
  NAME boxers.
  NAME my white boxers.
  INDEFINITE ARTICLE "your"
  DEFINITE ARTICLE "your"
  HAS ex "Just your boxer shorts (the loose type), white and plain.".
END THE hero_boxers.

THE hero_undershirt IsA cl_undershirt IN hero.
  IS worn.
  NAME undershirt.
  NAME my white undershirt.
  INDEFINITE ARTICLE "your"
  DEFINITE ARTICLE "your"
  HAS ex "A plain white T-shaped undershirt, a few stains here and there.".
END THE.

THE hero_shoes IsA cl_shoes IN hero.
  IS worn.
  NAME sneakers.
  NAME my black converse sneakers.
  INDEFINITE ARTICLE "your"
  DEFINITE ARTICLE "your"
  HAS ex "Your old and beloved black Converse sneakers, a bit worn out.".
END THE.

--------------------------------------------------------------------------------
-- Trousers
--------------------------------------------------------------------------------


THE hero_trousers IsA proto_trousers IN hero.
  IS worn.
  NAME khakis.
  NAME my khakis.
  NAME khaki pants.
  INDEFINITE ARTICLE "your"
  DEFINITE ARTICLE "your"
  HAS ex "A pair of khaki beige cotton pants.".
  -- -------------------------------------
  -- Customize Description of Its Contents
  -- -------------------------------------
  CONTAINER
    HEADER
      "Inside the khakis pockets there"
      IF COUNT IsA thing, DIRECTLY IN THIS >1
        THEN "are"
        ELSE "is"
      END IF.
END THE.

-- Clothes are also containers (to allow pockets, etc.).
-- We'll add some things the Hero's trousers to test how their are dealt with...

-- ----------
-- The Wallet
-- ----------
THE wallet IsA object IN hero_trousers.
  INDEFINITE ARTICLE "your"
  DEFINITE ARTICLE "your"
  HAS ex "Your leather wallet.".
END THE wallet.


-- ----------
-- The Kippah
-- ----------
-- We also add a wearable item in the trousers, to check that it's seen as being
-- carried and not worn.

THE kippah IsA clothing IN hero_trousers.
  HAS head_cover 2.
  INDEFINITE ARTICLE "your"
  DEFINITE ARTICLE "your"
  HAS ex "It's your kippah. You always carry it around with you.".
END THE kippah.


--------------------------------------------------------------------------------
START AT ega_entrance.
DESCRIBE banner.
"$pType" STYLE EMPHASIZED. "EXITS" STYLE NORMAL.
  "to view the list of exits of a room."
"$nUse the" STYLE EMPHASIZED. "DBG" STYLE NORMAL.
  "command (DGB <ITEM NAME>) to show the coverage values of any clothing item."

-- EOF --
